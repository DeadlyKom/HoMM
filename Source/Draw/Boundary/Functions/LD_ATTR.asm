
                ifndef _DRAW_BOUNDARY_SPRITE_NOT_CLIPPING_LOAD_ATTR_
                define _DRAW_BOUNDARY_SPRITE_NOT_CLIPPING_LOAD_ATTR_
; -----------------------------------------
; отображение двух пиксельных строк знакоместа
; In:
;   SP - адрес спрайта
;   HL - адрес экрана
;   D  - старший адрес таблицы зеркальных байт
;   BC - два байта спрайта
; Out:
; Corrupt:
; Note:
;   отображение производится вверху вниз
; -----------------------------------------
LD_2_LINES      macro
                LD E, C     ; первый байт всегда хранится отзеркаленый
                LD A, (DE)  ; зеркалим первый байт
                LD (HL), A
                INC H
                LD (HL), B  ; второй байт всегда хранится нормальный
                INC H
                POP BC      ; чтение двух байт спрайта
                endm
; -----------------------------------------
; отображение двух пиксельных строк знакоместа (завершающий)
; In:
;   SP - адрес спрайта
;   HL - адрес экрана
;   D  - старший адрес таблицы зеркальных байт
;   BC - два байта спрайта
; Out:
; Corrupt:
; Note:
;   отображение производится вверху вниз
; -----------------------------------------
LD_2_LINES_     macro
                LD E, C     ; первый байт всегда хранится отзеркаленый
                LD A, (DE)  ; зеркалим первый байт
                LD (HL), A
                INC H
                LD (HL), B  ; второй байт всегда хранится нормальный
                POP BC      ; чтение двух байт спрайта
                endm
; -----------------------------------------
; отображение спрайта с атрибутами
; In:
;   SP+0 - указывает на адрес спрайта
;   HL   - адрес экрана
;   BC   - два байта спрайта
; Out:
; Corrupt:
; Note:
; -----------------------------------------
LD_ATTR:        RESTORE_SCR_ADR
                LD_2_LINES
                LD_2_LINES
                LD_2_LINES
                LD_2_LINES_
                TO_ATTR_ADR
                PIXEL_DOWN_DE
                SET_ATTR
                JP (IY)
                
                display " - Draw sprite aligned in boundary 'LD ATTR':\t\t", /A, LD_ATTR, "\t= busy [ ", /D, $-LD_ATTR, " byte(s)  ]"

                endif ; ~ _DRAW_BOUNDARY_SPRITE_NOT_CLIPPING_LOAD_ATTR_
