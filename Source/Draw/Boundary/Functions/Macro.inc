
                ifndef _DRAW_BOUNDARY_SPRITE_NOT_CLIPPING_MACRO_
                define _DRAW_BOUNDARY_SPRITE_NOT_CLIPPING_MACRO_
; -----------------------------------------
; преобразование адрес экрана в атрибутный
; In:
;   SP - адрес спрайта
;   HL - адрес экрана
;   D  - старший адрес таблицы зеркальных байт
;   BC - два байта спрайта
; Out:
;   HL - адрес знакоместа атрибутов
;   DE - адрес экрана
; Corrupt:
;   DE
; Note:
;   отображение производится вверху вниз
; -----------------------------------------
TO_ATTR_ADR     macro
                EX DE, HL
                LD L, D
                LD H, HIGH Adr.ScrAttrAdrTable                                  ; адреса таблицы преобразование адресов экрана в атрибутные
                LD H, (HL)
                LD L, E
                endm
; -----------------------------------------
; восстановление адреса экрана
; In:
;   SP - адрес спрайта
;   HL - адрес знакоместа атрибутов
;   DE - адрес экрана
;   BC - два байта спрайта
; Out:
;   HL - адрес экрана
;   D  - старший адрес таблицы зеркальных байт
; Corrupt:
;   DE
; Note:
;   отображение производится вверху вниз
; -----------------------------------------
RESTORE_SCR_ADR macro
                EX DE, HL
                LD D, HIGH Adr.ByteMirrorTable                                  ; адреса таблицы зеркальных байт/буфера
                endm
; -----------------------------------------
; установка атрибута
; In:
;   SP - адрес спрайта
;   HL - адрес атрибутов
;   DE - адрес экрана
;   BC - два байта спрайта
; Out:
; Corrupt:
; Note:
; -----------------------------------------
SET_ATTR        macro
                LD (HL), C  ; запись байта в экран атрибутов
                LD A, B
                POP BC      ; чтение двух байт спрайта
                endm
; -----------------------------------------
; переход на знакоместо выше адрес пикселей
; In:
;   DE  - адрес экрана
; Out:
;   DE - адрес предыдущей строки
; Corrupt:
; Note:
;   отображение производится снизу вверх
; -----------------------------------------
PIXEL_UP_DE     macro
                DEC D
                LD A, E
                ADD A, #E0
                LD E, A
                SBC A, A
                AND #08
                ADD A, D
                LD D, A
                endm
; -----------------------------------------
; переход на знакоместо ниже адрес пикселей
; In:
;   DE  - адрес экрана
; Out:
;   DE - адрес следующей строки
; Corrupt:
; Note:
;   отображение производится вверху вниз
; -----------------------------------------
PIXEL_DOWN_DE   macro
                INC D
                LD A, E
                SUB #E0
                LD E, A
                SBC A, A
                AND #F8
                ADD A, D
                LD D, A
                endm

                endif ; ~ _DRAW_BOUNDARY_SPRITE_NOT_CLIPPING_MACRO_
