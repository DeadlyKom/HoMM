                ifndef _DRAW_WORLD_BACKGROUND_DRAW_COLUMN_
                define _DRAW_WORLD_BACKGROUND_DRAW_COLUMN_
ColumnBegin     EQU $
; -----------------------------------------
; макросы для компактизации записи
; In:
;   HL' - не спользуется
;   DE' - адрес буфера отображения (RenderBuffer)
;   B'  - счётчик тайлов отрисовки по вертикали
;   C'  - cчётчик тайлов отрисовки по горизонтали
;   HL  - адрес экрана вывода
;   DE  - не спользуется
;   BC  - первый два байта спрайта
;   SP  - адрес спрайта +2 батй
;   IX  - адрес функции базового отображения
;   IY  - адрес функции завершающего отображения
; Out:
; Corrupt:
; Note:
; -----------------------------------------
LEFT_2_LINES    macro
                LD (HL), B
                INC H
                POP BC
                LD (HL), C
                INC H
                POP BC
                endm
LEFT_2_LINES_   macro
                LD (HL), B
                INC H
                POP BC
                LD (HL), C
                ; INC H
                POP BC
                endm
LEFT_2_ATTR     macro
                LD (HL), B
                EX DE, HL
                INC H
                POP BC
                endm
CENTER_2_LINES  macro
                LD (HL), C
                INC L
                LD (HL), B
                INC H
                POP BC
                LD (HL), C
                DEC L
                LD (HL), B
                INC H
                POP BC
                endm
CENTER_2_LINES_ macro
                LD (HL), C
                INC L
                LD (HL), B
                INC H
                POP BC
                LD (HL), C
                DEC L
                LD (HL), B
                ; INC H
                POP BC
                endm
CENTER_2_ATTR   macro
                LD (HL), C
                INC L
                LD (HL), B
                EX DE, HL
                INC H
                POP BC
                endm
RIGHT_2_LINES   macro
                LD (HL), C
                INC H
                POP BC
                LD (HL), B
                INC H
                POP BC
                endm
RIGHT_2_LINES_  macro
                LD (HL), C
                INC H
                POP BC
                LD (HL), B
                ; INC H
                POP BC
                endm
RIGHT_2_ATTR    macro
                LD (HL), C
                EX DE, HL
                INC H
                POP BC
                endm
TO_ATTR_ADR     macro
                LD D, H
                LD E, L

                ; преобразование адрес экрана в атрибутный
                LD L, D
                LD H, HIGH Adr.ScrAttrAdrTable
                LD H, (HL)
                LD L, E

                endm
NEXT_ROW        macro
                LD A, L
                SUB #E0
                LD L, A
                SBC A, A
                AND #F8
                ADD A, H
                LD H, A
                endm
; -----------------------------------------
; отображение части колонки левее от целой
; In:
;   DE  - адрес буфера отображения (RenderBuffer)
;   B   - счётчик тайлов отрисовки по вертикали
;   C   - cчётчик тайлов отрисовки по горизонтали
;   DE' - адрес экрана вывода
;   IX  - адрес функции базового отображения
;   IY  - адрес функции завершающего отображения
; Out:
; Corrupt:
; Note:
;   установить RESTORE_BC
;   установить 7 страницу
;   устойчивый к прерываниям
; -----------------------------------------
Left:           ; чтение индекса тайла c карты
                LD A, (DE)
                INC E
                EXX

                ; избавление от старшего бита
                ADD A, A    ; x2
                LD L, A
                ; -----------------------------------------
                ;   значение в регистре L
                ;
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | T6 | T5 | T4 | T3 | T2 | T1 | T0 |  0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   T6-T0   [7..1]  - номер тайла от 0 до 127
                ; -----------------------------------------

                LD H, HIGH Adr.ArrayAdrTileSpr                                  ; старший адрес массива адресов спрайтов тайлов
                ; -----------------------------------------
                ;   значение в регистре H
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | 1  | 1  | A5 | A4 | A3 | A2 | A1 | A0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   A5-A0   [7..2]  - адрес таблицы (13..8 биты адреса)
                ; -----------------------------------------

                LD A, (HL)
                INC L
                LD H, (HL)
.SprOffset      EQU $+1
                ADD A, #00                                                      ; смещение в спрайте
                LD L, A
                ADC A, H
                SUB L
                LD H, A

                ; -----------------------------------------
                ; защита от повреждения данных во время прерывания
                ; -----------------------------------------

                ; копирование данных 2-х байт спрайта
                LD C, (HL)
                INC L
                LD B, (HL)
                INC L

                ; инициализация стека, указывает на спрайт тайла
                LD SP, HL
                EX DE, HL

                ; HL' - не используется
                ; DE' - адрес буфера отображения (RenderBuffer)
                ; B'  - счётчик тайлов отрисовки по вертикали
                ; C'  - cчётчик тайлов отрисовки по горизонтали
                ; HL  - адрес экрана вывода
                ; DE  - не используется
                ; BC  - первый два байта спрайта
                ; SP  - адрес спрайта +2 байта (ранее считаный)
                ; IX  - адрес функции базового отображения
                ; IY  - адрес функции завершающего отображения

                ; -----------------------------------------
                ; основноый цикл отрисовки
                ; -----------------------------------------
.x16            LEFT_2_LINES
                LEFT_2_LINES
                LEFT_2_LINES
                LEFT_2_LINES_

                TO_ATTR_ADR
                LEFT_2_ATTR

.x08            NEXT_ROW
                LEFT_2_LINES
                LEFT_2_LINES
                LEFT_2_LINES
                LEFT_2_LINES_

                TO_ATTR_ADR
                LEFT_2_ATTR

.x00            NEXT_ROW
                JP .x16
.Diff           EQU $-.x16

                ; -----------------------------------------
                ; завершающий цикл отрисовки
                ; -----------------------------------------
                LEFT_2_LINES
                LEFT_2_LINES
                LEFT_2_LINES
                LEFT_2_LINES_

                TO_ATTR_ADR
                LEFT_2_ATTR

                NEXT_ROW
                LEFT_2_LINES
                LEFT_2_LINES
                LEFT_2_LINES
                LEFT_2_LINES_

                TO_ATTR_ADR
                LEFT_2_ATTR

                NEXT_ROW
.ContainerSP    EQU $+1
                LD SP, #0000
                
                POP DE                                                          ; восстановление адреса экрана
                INC E                                                           ; следующая колонка
                PUSH DE
                EXX

                ; INC E                                                           ; не понятный переход к новому тайлу в буфере (пропуск) !!!!!!!!!!!!
; -----------------------------------------
; отображение целых колонок
; In:
;   DE  - адрес буфера отображения (RenderBuffer)
;   B   - счётчик тайлов отрисовки по вертикали
;   C   - cчётчик тайлов отрисовки по горизонтали
;   DE' - адрес экрана вывода
;   IX  - адрес функции базового отображения
;   IY  - адрес функции завершающего отображения
; Out:
; Corrupt:
; Note:
;   установить RESTORE_BC
;   установить 7 страницу
;   устойчивый к прерываниям
; -----------------------------------------
Whole:          ; инициализация основного цикла отображения
.BasicDraw      EQU $+1
                LD HL, #0000
                LD (ColumnDraw.BasicDraw), HL

                ; инициализация завершающего отображения
.FinalDraw      EQU $+2
                LD IY, #0000

.Loop           LD B, SCR_WORLD_SIZE_Y
.Right          ; проверка обнуления счётчика тайлов по горизонтали
                DEC C
                JP Z, Restore                                                   ; переход, если счётчик тайлов отрисовки по горизонтали обнулён

                ; чтение индекса тайла карты
                LD A, (DE)
                INC E
                EXX

                ; избавление от старшего бита
                ADD A, A    ; x2
                LD L, A
                ; -----------------------------------------
                ;   значение в регистре L
                ;
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | T6 | T5 | T4 | T3 | T2 | T1 | T0 |  0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   T6-T0   [7..1]  - номер тайла от 0 до 127
                ; -----------------------------------------

                LD H, HIGH Adr.ArrayAdrTileSpr                                  ; старший адрес массива адресов спрайтов тайлов
                ; -----------------------------------------
                ;   значение в регистре H
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | 1  | 1  | A5 | A4 | A3 | A2 | A1 | A0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   A5-A0   [7..2]  - адрес таблицы (13..8 биты адреса)
                ; -----------------------------------------
                LD A, (HL)
                INC L
                LD H, (HL)
.SprOffset      EQU $+1
                ADD A, #00                                                      ; смещение в спрайте
                LD L, A
                ADC A, H
                SUB L
                LD H, A

                ; -----------------------------------------
                ; защита от повреждения данных во время прерывания
                ; -----------------------------------------

                ; копирование данных 2-х байт спрайта
                LD C, (HL)
                INC L
                LD B, (HL)
                INC L

                ; инициализация стека, указывает на спрайт тайла
                LD SP, HL
                EX DE, HL

                ; HL' - не используется
                ; DE' - адрес буфера отображения (RenderBuffer)
                ; B'  - счётчик тайлов отрисовки по вертикали
                ; C'  - cчётчик тайлов отрисовки по горизонтали
                ; HL  - адрес экрана вывода
                ; DE  - не используется
                ; BC  - первый два байта спрайта
                ; SP  - адрес спрайта +2 байта (ранее считаный)
                ; IX  - адрес функции базового отображения
                ; IY  - адрес функции завершающего отображения

                ; -----------------------------------------
                ; основноый цикл отрисовки
                ; -----------------------------------------
.x16            CENTER_2_LINES
                CENTER_2_LINES
                CENTER_2_LINES
                CENTER_2_LINES_

                TO_ATTR_ADR
                CENTER_2_ATTR

.x08            NEXT_ROW
                CENTER_2_LINES
                CENTER_2_LINES
                CENTER_2_LINES
                CENTER_2_LINES_

                TO_ATTR_ADR
                CENTER_2_ATTR

.x00            NEXT_ROW
                JP .x16
.Diff           EQU $-.x16

                ; -----------------------------------------
                ; завершающий цикл отрисовки
                ; -----------------------------------------
                CENTER_2_LINES
                CENTER_2_LINES
                CENTER_2_LINES
                CENTER_2_LINES_

                TO_ATTR_ADR
                CENTER_2_ATTR

                NEXT_ROW
                CENTER_2_LINES
                CENTER_2_LINES
                CENTER_2_LINES
                CENTER_2_LINES_

                TO_ATTR_ADR
                CENTER_2_ATTR

                NEXT_ROW

.ContainerSP    EQU $+1
                LD SP, #0000
                
                POP DE                                                          ; восстановление адреса экрана
                INC E                                                           ; следующая колонка
                INC E                                                           ; следующая колонка
                PUSH DE
                EXX

                ; INC E                                                           ; не понятный переход к новому тайлу в буфере (пропуск) !!!!!!!!!!!!

                ; проверка обнуления счётчика тайлов по вертикали
                DEC C
                JP NZ, .Loop
; -----------------------------------------
; отображение части колонки правее от целой
; In:
;   DE  - адрес буфера отображения (RenderBuffer)
;   B   - счётчик тайлов отрисовки по вертикали
;   C   - cчётчик тайлов отрисовки по горизонтали
;   DE' - адрес экрана вывода
;   IX  - адрес функции базового отображения
;   IY  - адрес функции завершающего отображения
; Out:
; Corrupt:
; Note:
;   установить RESTORE_BC
;   установить 7 страницу
;   устойчивый к прерываниям
; -----------------------------------------
Right:          ; инициализация основного цикла отображения
.BasicDraw      EQU $+1
                LD HL, #0000
                LD (ColumnDraw.BasicDraw), HL

                ; инициализация завершающего отображения
.FinalDraw      EQU $+2
                LD IY, #0000

                LD B, SCR_WORLD_SIZE_Y

                ; чтение индекса тайла карты
                LD A, (DE)
                INC E
                EXX

                ; избавление от старшего бита
                ADD A, A    ; x2
                LD L, A
                ; -----------------------------------------
                ;   значение в регистре L
                ;
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | T6 | T5 | T4 | T3 | T2 | T1 | T0 |  0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   T6-T0   [7..1]  - номер тайла от 0 до 127
                ; -----------------------------------------

                LD H, HIGH Adr.ArrayAdrTileSpr                                  ; старший адрес массива адресов спрайтов тайлов
                ; -----------------------------------------
                ;   значение в регистре H
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | 1  | 1  | A5 | A4 | A3 | A2 | A1 | A0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   A5-A0   [7..2]  - адрес таблицы (13..8 биты адреса)
                ; -----------------------------------------

                LD A, (HL)
                INC L
                LD H, (HL)
.SprOffset      EQU $+1
                ADD A, #00                                                      ; смещение в спрайте
                LD L, A
                ADC A, H
                SUB L
                LD H, A

                ; -----------------------------------------
                ; защита от повреждения данных во время прерывания
                ; -----------------------------------------

                ; копирование данных 2-х байт спрайта
                LD C, (HL)
                INC L
                LD B, (HL)
                INC L

                ; инициализация стека, указывает на спрайт тайла
                LD SP, HL
                EX DE, HL

                ; HL' - не используется
                ; DE' - адрес буфера отображения (RenderBuffer)
                ; B'  - счётчик тайлов отрисовки по вертикали
                ; C'  - cчётчик тайлов отрисовки по горизонтали
                ; HL  - адрес экрана вывода
                ; DE  - не используется
                ; BC  - первый два байта спрайта
                ; SP  - адрес спрайта +2 байта (ранее считаный)
                ; IX  - адрес функции базового отображения
                ; IY  - адрес функции завершающего отображения

                ; -----------------------------------------
                ; основноый цикл отрисовки
                ; -----------------------------------------
.x16            RIGHT_2_LINES
                RIGHT_2_LINES
                RIGHT_2_LINES
                RIGHT_2_LINES_

                TO_ATTR_ADR
                RIGHT_2_ATTR

.x08            NEXT_ROW
                RIGHT_2_LINES
                RIGHT_2_LINES
                RIGHT_2_LINES
                RIGHT_2_LINES_

                TO_ATTR_ADR
                RIGHT_2_ATTR

.x00            NEXT_ROW
                JP .x16
.Diff           EQU $-.x16

                ; -----------------------------------------
                ; завершающий цикл отрисовки
                ; -----------------------------------------
                RIGHT_2_LINES
                RIGHT_2_LINES
                RIGHT_2_LINES
                RIGHT_2_LINES_

                TO_ATTR_ADR
                RIGHT_2_ATTR

                NEXT_ROW
                RIGHT_2_LINES
                RIGHT_2_LINES
                RIGHT_2_LINES
                RIGHT_2_LINES_

                TO_ATTR_ADR
                RIGHT_2_ATTR

                NEXT_ROW

.ContainerSP    EQU $+1
                LD SP, #0000
Restore:        ; -----------------------------------------
                ; восстановление ранее модифицированный код
                ; -----------------------------------------
                POP HL                                                          ; удаление адреса буфера со стека
                LD HL, #C124                                                    ; INC H : POP BC
.BasicDraw      EQU $+1
                LD (#0000), HL
          
.FinalDraw      EQU $+1
                LD (#0000), HL
                RET

; -----------------------------------------
; отображение колонки
; In:
;   HL  - адрес экрана вывода
;   DE' - адрес буфера отображения (RenderBuffer)
;   B'  - счётчик тайлов отрисовки по вертикали
;   C'  - cчётчик тайлов отрисовки по горизонтали
;   IX  - адрес функции базового отображения
;   IY  - адрес функции завершающего отображения
; Out:
; Corrupt:
; Note:
;   установить RESTORE_BC
;   установить 7 страницу
;   устойчивый к прерываниям
; -----------------------------------------
ColumnDraw:     ; инициализация
.ContainerSP    EQU $+1
                LD SP, #0000
                INC H                                                           ; переход на следующую строку

                EXX

                ; чтение индекса тайла карты
                LD A, (DE)
                INC E

                DEC B                                                           ; уменьшить счётчик тайлов отрисовки по вертикали

                EXX
                EX DE, HL

                ; избавление от старшего бита
                LD L, A
                EX AF, AF'
                SLA L

                ; -----------------------------------------
                ;   значение в регистре L
                ;
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | T6 | T5 | T4 | T3 | T2 | T1 | T0 |  0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   T6-T0   [7..1]  - номер тайла от 0 до 127
                ; -----------------------------------------

                LD H, HIGH Adr.ArrayAdrTileSpr                                  ; старший адрес массива адресов спрайтов тайлов
                ; -----------------------------------------
                ;   значение в регистре H
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | 1  | 1  | A5 | A4 | A3 | A2 | A1 | A0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   A5-A0   [7..2]  - адрес таблицы (13..8 биты адреса)
                ; -----------------------------------------

                LD A, (HL)
                INC L
                LD H, (HL)
                LD L, A

                ; -----------------------------------------
                ; защита от повреждения данных во время прерывания
                ; -----------------------------------------

                ; копирование данных 2-х байт спрайта
                LD C, (HL)
                INC L
                LD B, (HL)
                INC L
                EX AF, AF'

                ; инициализация стека, указывает на спрайт тайлаёёёёё
                LD SP, HL
                EX DE, HL

                ; HL' - не используется
                ; DE' - адрес буфера отображения (RenderBuffer)
                ; B'  - счётчик тайлов отрисовки по вертикали
                ; C'  - cчётчик тайлов отрисовки по горизонтали
                ; HL  - адрес экрана вывода
                ; DE  - не используется
                ; BC  - первый два байта спрайта
                ; SP  - адрес спрайта +2 байта (ранее считаный)
                ; IX  - адрес функции базового отображения
                ; IY  - адрес функции завершающего отображения

.BasicDraw      EQU $+1
                JP NZ, $                                                        ; переход базовой функции отрисовки
.FinalDraw      JP (IY)                                                         ; переход завершающей функции отрисовки

                display " - Draw column:\t\t\t\t\t", /A, ColumnBegin, "\t= busy [ ", /D, $-ColumnBegin, " byte(s)  ]"

                endif ; ~ _DRAW_WORLD_BACKGROUND_DRAW_COLUMN_
