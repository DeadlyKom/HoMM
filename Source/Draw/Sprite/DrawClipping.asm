
                ifndef _DRAW_SPRITE_DRAW_CLIPPING_
                define _DRAW_SPRITE_DRAW_CLIPPING_
; -----------------------------------------
; отсечение спрайта с последующим отображение
; In:
;   HL - адрес спрайта FSprite/FSpritesRef
;   флаг переполнения указывает на структуру FSpritesRef в регистровой паре HL,
;   a в аккумуляторе хранится индекс спрайта
; Out:
; Corrupt:
;   HL, DE, BC, AF, HL', DE', BC', AF', IX, IY, SP
; Note:
;   - отражённые спрайты корректно будут обрабатываться только,
;    если доступ к ним через FSpritesRef
;   - отражённые спрайты должны иметь ширину кратную 8
; -----------------------------------------
DrawClipping:   ; --------------------------------------------------------------
                ; *вставка для отражающего спрайта
                EX AF, AF'
                XOR A
                EX AF, AF'
                ; --------------------------------------------------------------

                JR NC, .FSprite                                                 ; переход, если HL указывает на FSprite
                INC L                                                           ; пропуск FSpritesRef.Num

                ; чтение страницы расположения данных о структурах
                EX AF, AF'
                LD A, (HL)                                                      ; FSpritesRef.Data.Page
                INC L
                SET_PAGE_A                                                      ; установка страницы спрайта
                EX AF, AF'

                ; чтение адреса расположения массива структур
                LD E, (HL)
                INC L
                LD D, (HL)
            
                ; корректировка адреса расположения необходимой структуры FSprite
                LD L, A
                LD H, #00
                ADD HL, HL  ; x2
                ADD HL, HL  ; x4
                ADD HL, HL  ; x8
                ADD HL, DE

                ; копирование структуры
                SPRITE_DE SPRITE_TEMP_IDX
                LDI ; FSpriteInfo.Width
                LDI ; FSpriteInfo.Height
                LDI ; FSprite.Info.SOx
                LDI ; FSprite.Info.SOy
                LDI ; FSprite.ExtraFlags
                ; --------------------------------------------------------------
                ; *вставка для отражающего спрайта
                LD A, (HL)                                                      ; чтение FSprite.Data.Page
                ; --------------------------------------------------------------
                LDI ; FSprite.Data.Page
                LDI ; FSprite.Data.AdrL
                LDI ; FSprite.Data.AdrH
                SPRITE_HL SPRITE_TEMP_IDX
                EX AF, AF'

.FSprite        ; чтение размера спрайта
                LD C, (HL)                                                      ; FSpriteInfo.Width
                INC L                                                           ; FSprite выровнен по 8 байт
                LD B, (HL)                                                      ; FSpriteInfo.Height
                INC L                                                           ; FSprite выровнен по 8 байт

                ; чтение смещения по горизонтали
                LD A, (HL)                                                      ; FSpriteInfo.SOx
                INC L                                                           ; FSprite выровнен по 8 байт

                PUSH BC
                EXX
                POP BC

                ; -----------------------------------------
.HorizontClip   ; горизонтальное отсечение (начало)
                ; -----------------------------------------
                ; BC - размер спрайта (B - изначальная высота, С - ширина) в пикселях
                ; A  - смещение по горизонтали
                ; -----------------------------------------

                ; -----------------------------------------
                ; приведение к форме позиции 12.4 (по горизонтали)
                ; -----------------------------------------
                LD L, A
                ADD A, A
                SBC A, A
                LD H, A

                ; SOx <<= 4
                ADD HL, HL  ; x2
                ADD HL, HL  ; x4
                ADD HL, HL  ; x8
                ADD HL, HL  ; x16

                ; HL = SOx + Position.X
.PositionX      EQU $+1
                LD DE, #0000                                                    ; Position.X (позиция спрайта в мире)
                ADD HL, DE

                ; проверка достежения левой грани
                EX DE, HL
                LD A, (GameState.LeftEdge)
                LD L, A
                LD H, #00
                ADD HL, HL  ; x2
                ADD HL, HL  ; x4
                ADD HL, HL  ; x8
                ADD HL, HL  ; x16
                EX DE, HL

                SBC HL, DE
                JP M, .LeftClip                                                 ; переход, если спрайт лежит левее относительно левой грани

                ; -----------------------------------------
.RightClip      ; проверка положения спрайта относительно правой грани
                ; -----------------------------------------
                ; HL - положение спрайта (его левой грани) в пикселя, относительно видимой части (левой грани)
                ; BC - размер спрайта (B - изначальная высота, С - ширина) в пикселях
                ; -----------------------------------------

                ; преобразование позиции спрайта 12.4 к 8-битному (экранное значение)
                ADD HL, HL  ; x2
                LD E, H     ; начальная позиция в знакоместах
                ADD HL, HL  ; x4
                ADD HL, HL  ; x16
                ADD HL, HL  ; x32
                ; H - позиция спрайта по горизонтали в пикселях (левая грань спрайта)
                RET C                                                           ; выход, если спрайт находится правее относительно правой грани

                LD A, (GameState.VisibleWidth)                                  ; ширина видимой области (в знакоместах)
                LD L, A
                LD A, E
                SUB L
                RET NC                                                          ; выход, если спрайт находится правее относительно правой грани

                ; --------------------------------------------------------------
                ; *вставка для отражающего спрайта
                ; смещение видимой области
                EX AF, AF'
                LD E, A                                                         ; аккумулятор хранит FSprite.Data.Page
                
                ; расчёт положения спрайта на экране в пикселях
                LD A, (GameState.LeftEdge)
                ADD A, H
                LD D, A                                                         ; положение спрайта на экране

                ; проверка флага, отражение спрайта
                BIT SPR_MIRROR_BIT, E
                JR Z, .SavePositionR                                            ; переход, если спрайт не отражённый

                ; спрайт является отражающим, но если он выровнен по знакоместу
                ; сместимся на 1 знакоместо раньше (необходимо для последующего корректно расчёта)
                ; LD A, D
                AND #07
                LD A, D
                JR NZ, .SavePositionR                                           ; переход, если положение не выровнено по знакоместу

                ; сместимся на 1 знакоместо раньше (необходимо для последующего корректно расчёта)
                LD A, D
                SUB #08
                LD D, A

.SavePositionR  EX AF, AF'
                ; --------------------------------------------------------------
                ; расчёт ширины невидимой части спрайта в пикселях
                LD A, L                                                         ; ширина видимой области (в знакоместах)
                ADD A, A    ; x2
                ADD A, A    ; x4
                ADD A, A    ; x8
                SUB H                                                           ; положение спрайта по горизонтали в пикселях
                NEG                                                             ; отрицательная доступная ширина видимой области
                ADD A, C                                                        ; сумма отрицательной доступной ширины и ширины спрайта в пикселях
                LD L, A
                LD H, C                                                         ; ширина спрайта в пикселях
                LD E, #00                                                       ; нулевое смещение в таблице
                JR NC, .HorizontClipped                                         ; переход, если спрайт полностью помещается на экране
                ; дополнение ширины спрайта,
                ; для корректной выборки в таблице по ширене спрайтов
                LD A, C
                NEG
                AND #07
                ADD A, L
                LD D, A

                ; округление до знакоместа (положительное число)
                SRL A
                ADC A, #00
                RRA
                ADC A, #00
                RRA
                ADC A, #00
                LD E, A

                ; ширина спрайта
                LD A, C
                SUB L
                LD H, A                                                         ; новая ширина спрайта в пикселях

                ; A' - позиция спрайта по горизонтали в пикселях
                ; D  - ширина невидимой части спрайта в пикселях (+)
                ; E  - ширина невидимой части спрайта в знакоместах (+)
                JR .HorizontClipped

.LeftClip       ; -----------------------------------------
                ; спрайта находится левее относительно левой грани
                ; -----------------------------------------
                ; HL - положение спрайта (его левой грани) в пикселя, относительно видимой части тайловой карты
                ; BC - размер спрайта (B - изначальная высота, С - ширина) в пикселях
                ; -----------------------------------------

                ; преобразование ширины спрайта к 16-битному
                LD D, #00
                LD A, C     ; xxxwwwww
                ADD A, A    ; xxwwwww0
                ADD A, A    ; xwwwww00
                ADD A, A    ; wwwww000 : w
                RL D        ; 0000000w wwwww000
                ADD A, A    ; wwww0000 : w
                RL D        ; 000000ww wwww0000
                LD E, A

                ADC HL, DE                                                      ; флаг С сброшен
                RET M                                                           ; выход, если спрайт находится левее относительно левой грани

                ; преобразование позиции спрайта 12.4 к 8-битному (экранное значение)
                ADD HL, HL  ; x2
                ADD HL, HL  ; x4
                ADD HL, HL  ; x16
                ADD HL, HL  ; x32
                ; H - позиция спрайта по горизонтали в пикселях (правая грань спрайта)

                LD A, H
                OR A
                RET Z                                                           ; выход, если спрайт находится левее относительно левой грани

                ; расчёт ширины невидимой части спрайта в пикселях
                SUB C
                EX AF, AF'                                                      ; округление до знакоместа будет позже
                
                ; --------------------------------------------------------------
                ; *вставка для отражающего спрайта

                ; проверка флага, отражение спрайта
                ; аккумулятор хранит FSprite.Data.Page
                BIT SPR_MIRROR_BIT, A
                JR Z, .SetLeftEdge                                              ; переход, если спрайт не отражённый

                ; спрайт является отражающим, но если он выровнен по знакоместу
                ; сместимся на 1 знакоместо раньше (необходимо для последующего корректно расчёта)
                LD A, H
                AND #07
                JR NZ, .SetLeftEdge                                             ; переход, если положение не выровнено по знакоместу

                ; сместимся на 1 знакоместо раньше (необходимо для последующего корректно расчёта)
                LD A, (GameState.LeftEdge)
                SUB #08
                JR .SavePositionL

.SetLeftEdge    ; смещение видимой области
                LD A, (GameState.LeftEdge)
.SavePositionL  EX AF, AF'

                ; A' - позиция спрайта по горизонтали в пикселях
                ; A  - ширина невидимой части спрайта в знакоместах (-)

                LD D, A
                ; округление до знакоместа (отрицательное число)
                SRA A
                SRA A
                SRA A
                LD E, A

                ; -----------------------------------------
.HorizontClipped; горизонтальное отсечение (завершение)
                ; -----------------------------------------
                ; A' - позиция спрайта по горизонтали в пикселях
                ; D  - ширина невидимой части спрайта в пикселях (-/+)
                ; E  - ширина невидимой части спрайта в знакоместах (-/+)
                ; H  - новая ширина спрайта в пикселях
                ; BC - размер спрайта (B - изначальная высота, С - ширина) в пикселях
                ; -----------------------------------------

                ; округление до знакоместах
                LD A, C                                                         ; ширины спрайта в пикселях
                LD C, #00
                SRL A
                ADC A, C
                RRA
                ADC A, C
                RRA
                ADC A, C
                LD C, A

                LD A, H
                PUSH DE
                EXX
                POP BC
                LD D, A     ; новая ширина спрайта в пикселях
                LD A, B
                EX AF, AF'
                LD E, A                                                         ; сохранение позиции спрайта по горизонтали в пикселях

                ; чтение смещения по вертикали
                LD A, (HL)                                                      ; FSpriteInfo.SOy
                INC L                                                           ; FSprite выровнен по 8 байт

                EXX
                ; -----------------------------------------
.VerticalClip   ; вертекальное отсечение
                ; -----------------------------------------
                ; BC  - размер спрайта (B - изначальная высота, С - ширина) в пикселях
                ; A  - смещение по вертикали
                ; -----------------------------------------

                ; -----------------------------------------
                ; преобразование к форме позиции 12.4 (по вертикали)
                ; -----------------------------------------
                LD L, A
                ADD A, A
                SBC A, A
                LD H, A

                ; SOy <<= 4
                ADD HL, HL  ; x2
                ADD HL, HL  ; x4
                ADD HL, HL  ; x8
                ADD HL, HL  ; x16

                ; HL = SOy + Position.Y
.PositionY      EQU $+1
                LD DE, #0000                                                    ; Position.Y (позиция спрайта в мире)
                ADD HL, DE

                ; проверка достежени верхней грани
                EX DE, HL
                LD A, (GameState.TopEdge)
                LD L, A
                LD H, #00
                ADD HL, HL  ; x2
                ADD HL, HL  ; x4
                ADD HL, HL  ; x8
                ADD HL, HL  ; x16
                EX DE, HL
                SBC HL, DE
                JP M, .TopClip                                                  ; переход, если спрайт лежит выше относительно верхней грани

                ; -----------------------------------------
.BottomClip     ; проверка положения спрайта относительно нижней грани
                ; -----------------------------------------
                ; HL - положение спрайта (его верхней грани) в пикселя, относительно видимой части тайловой карты
                ; BC - размер спрайта (B - изначальная высота, С - ширина) в пикселях
                ; -----------------------------------------

                ; преобразование позиции спрайта 12.4 к 8-битному (экранное значение)
                ADD HL, HL  ; x2
                ADD HL, HL  ; x4
                ADD HL, HL  ; x16
                ADD HL, HL  ; x32
                ; H - позиция спрайта по вертикали в пикселях (верхняя грань спрайта)
                RET C                                                           ; выход, если спрайт находится ниже относительно нижней грани

                LD A, (GameState.VisibleHeight)                                 ; высота видимой области SCR_WORLD_SIZE_Y << 3
                SUB H
                RET C                                                           ; выход, если спрайт находится ниже относительно нижней грани
                RET Z                                                           ; выход, если спрайт находится на нижней грани

                ; смещение видимой области
                EX AF, AF'
                LD D, A
                LD A, (GameState.TopEdge)
                ADD A, H
                EX AF, AF'
                
                LD L, #00                                                       ; высота невидимой части спрайта в пикселях

                ; расчёт высоты невидимой части спрайта в пикселях
                SUB B
                JR NC, .VerticalClipped                                         ; переход, если спрайт полностью помещается на экране

                ; корректировка высоты спрайта
                ADD A, B
                LD B, A

                ; A' - позиция спрайта по вертикали в пикселях
                ; B  - новая высота видимой части спрайта в пикселях (+)
                ; L  - высота невидимой части спрайта в пикселях
                JR .VerticalClipped

                ; -----------------------------------------
.TopClip        ; спрайта находится выше относительно верхней грани
                ; -----------------------------------------
                ; HL - положение спрайта (его верхней грани) в пикселя, относительно видимой части тайловой карты
                ; BC - размер спрайта (B - изначальная высота, С - ширина) в пикселях
                ; -----------------------------------------

                ; преобразование высоты спрайта к 16-битному
                LD D, #00
                LD A, B     ; xxxwwwww
                ADD A, A    ; xxwwwww0
                ADD A, A    ; xwwwww00
                ADD A, A    ; wwwww000 : w
                RL D        ; 0000000w wwwww000
                ADD A, A    ; wwww0000 : w
                RL D        ; 000000ww wwww0000
                LD E, A

                ADC HL, DE                                                      ; флаг С сброшен
                RET M                                                           ; выход, если спрайт находится выше относительно верхней грани

                ; преобразование позиции спрайта 12.4 к 8-битному (экранное значение)
                ADD HL, HL  ; x2
                ADD HL, HL  ; x4
                ADD HL, HL  ; x16
                ADD HL, HL  ; x32
                ; H - позиция спрайта по вертикали в пикселях (нижняя грань спрайта)

                LD A, H
                OR A
                RET Z                                                           ; выход, если спрайт находится выше относительно верхней грани

                ; расчёт высоты невидимой части спрайта в пикселях
                LD L, A
                NEG
                ADD A, B
                LD B, L
                LD L, A

                ; смещение видимой области
                LD A, (GameState.TopEdge)
                EX AF, AF'
                LD D, A

                ; A' - позиция спрайта по вертикали в пикселях
                ; B  - новая высота спрайта видимой части спрайта в пикселях
                ; L  - высота невидимой части спрайта в пикселях

                ; -----------------------------------------
.VerticalClipped; вертикальное отсечение (завершение)
                ; -----------------------------------------
                ; A' - позиция спрайта по вертикали в пикселях
                ; B  - новая высота спрайта видимой/невидимой части спрайта в пикселях (-/+)
                ; C  - ширина спрайта в знакоместах
                ; L  - высота невидимой части спрайта в пикселях
                ; C' - ширина невидимой части спрайта в знакоместах (-/+)
                ; D  - ширина невидимой части спрайта в пикселях (-/+)
                ; D' - новая ширина спрайта в пикселях
                ; E  - позиции спрайта по горизонтали в пикселях
                ; -----------------------------------------
                LD A, B
                EXX
                LD B, A

                INC L                                                           ; пропуск FSprite.ExtraFlags

                ; -----------------------------------------
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | D1 | D0 | MR | P4 | P3 | P2 | P1 | P0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   D1,D0   [7,6]       - тип вывода спрайта
                ;                           00 - OR & XOR с сохранением фона
                ;                           01 - OR & XOR
                ;                           10 - LD с атрибутами
                ;                           11 - OR & XOR с атрибутами
                ;   MR      [5]         - флаг, зеркального отображения спрайта по горизонтали
                ;   P4-P0   [4..0]      - страница хранения спрайта (32 страницы)
                ; -----------------------------------------
                LD A, (HL)                                                      ; FSpriteData.Page
                INC L                                                           ; FSprite выровнен по 8 байт
                LD (.Flags), A
                AND %00011111
                PUSH BC
                SET_PAGE_A                                                      ; установка страницы спрайта
                POP BC

                ; чтение адреса спрайта
                LD A, (HL)                                                      ; FSpriteData.Adr
                INC L                                                           ; FSprite выровнен по 8 байт
                LD H, (HL)                                                      ; FSpriteData.Adr
                LD L, A
                PUSH HL                                                         ; сохранение адреса спрайта
                LD A, D     ; новая ширина спрайта в пикселях
                EXX
                LD B, A
                LD A, D
                POP DE                                                          ; восстановление адреса спрайта
                EXX

                ; рассчёт адреса экранной области
                LD H, HIGH Adr.ScrAdrTable
                EX AF, AF'
                LD L, A                                                         ; позиции спрайта по вертикали в пикселях
                EXX
                LD H, A
                EXX
                LD A, (HL)
                INC H
                LD D, (HL)
                ; корректировка адреса по горизонтали
                INC H
                LD L, E
                OR (HL)
                LD E, A
                LD A, L
                PUSH AF

                ; корректировка адреса вывода
                LD A, (GameState.Screen)
                XOR D
                AND %10000000
                XOR D
                LD D, A

                ; флаги спрайта
.Flags          EQU $+1
                LD A, #00
                ; -----------------------------------------
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | D1 | D0 | MR | P4 | P3 | P2 | P1 | P0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   D1,D0   [7,6]       - тип вывода спрайта
                ;                           00 - OR & XOR с сохранением фона
                ;                           01 - OR & XOR
                ;                           10 - LD с атрибутами
                ;                           11 - OR & XOR с атрибутами
                ;   MR      [5]         - флаг, зеркального отображения спрайта по горизонтали
                ;   P4-P0   [4..0]      - страница хранения спрайта (32 страницы)
                ; -----------------------------------------
                
                RRA
                RRA
                RRA
                RRA
                AND %00001110
                ADD A, LOW .JumpTable
                LD L, A
                ADC A, HIGH .JumpTable
                SUB L
                LD H, A
                LD A, (HL)
                INC HL
                LD H, (HL)
                LD L, A
                POP AF
                JP (HL)
                
                ; -----------------------------------------
                ; тип вывода спрайта
                ; + 000 - OR & XOR с сохранением фона
                ; - 001 - OR & XOR с сохранением фона, отражённый спрайт по горизонтали
                ; + 010 - OR & XOR
                ; + 011 - OR & XOR, отражённый спрайт по горизонтали
                ; + 100 - LD с атрибутами
                ; - 101 - LD с атрибутами, отражённый спрайт по горизонтали
                ; + 110 - OR & XOR с атрибутами
                ; - 111 - OR & XOR с атрибутами, отражённый спрайт по горизонтали
                ; -----------------------------------------
.JumpTable      DW DrawOR_XOR_Save
                DW Exit
                DW DrawOR_XOR
                DW DrawM_OR_XOR
                DW DrawLD_ATTR
                DW Exit
                DW DrawOR_XOR_ATTR
                DW Exit
Exit:           ; выход
                RET

                display " - Draw function 'Clipping':\t\t\t\t", /A, DrawClipping, "\t= busy [ ", /D, $-DrawClipping, " byte(s)  ]"

                endif ; ~ _DRAW_SPRITE_DRAW_CLIPPING_
