
                ifndef _DRAW_UTILS_GET_SPRITE_AABB_
                define _DRAW_UTILS_GET_SPRITE_AABB_

                module Utilities
; -----------------------------------------
; получить ограничивающий прямоугольник спрайта
; In:
;   HL - адрес спрайта FSprite/FSpritesRef
;   флаг переполнения указывает на структуру FSpritesRef в регистровой паре HL,
;   a в аккумуляторе хранится индекс спрайта
; Out:
;   DE - позиция спрайта в пикселях (D - y, E - x)
;   BC - размер bound спрайта в пикселях (B - y, C - x)
;   флаг переполнения Carry установлен, если неудалось определить bound спрайта
; Corrupt:
; Note:
; -----------------------------------------
SpriteBound:    ; --------------------------------------------------------------
                ; *вставка для отражающего спрайта
                EX AF, AF'
                XOR A
                EX AF, AF'
                ; --------------------------------------------------------------
                
                JR NC, .FSprite                                                 ; переход, если HL указывает на FSprite
                INC L                                                           ; пропуск FSpritesRef.Num

                ; чтение страницы расположения данных о структурах
                EX AF, AF'
                LD A, (HL)                                                      ; FSpritesRef.Data.Page
                INC L
                SET_PAGE_A                                                      ; установка страницы спрайта
                EX AF, AF'

                ; чтение адреса расположения массива структур
                LD E, (HL)
                INC L
                LD D, (HL)
            
                ; корректировка адреса расположения необходимой структуры FSprite
                LD L, A
                LD H, #00
                ADD HL, HL  ; x2
                ADD HL, HL  ; x4
                ADD HL, HL  ; x8
                ADD HL, DE

                ; --------------------------------------------------------------
                ; *вставка для отражающего спрайта
                LD D, H
                LD A, L
                ADD A, FSprite.Data.Page
                LD E, A
                JR NC, $+3
                INC D
                LD A, (DE)                                                      ; чтение FSprite.Data.Page
                ; --------------------------------------------------------------
                EX AF, AF'

.FSprite        ; чтение размера спрайта
                LD C, (HL)                                                      ; FSpriteInfo.Width
                INC L                                                           ; FSprite выровнен по 8 байт
                LD B, (HL)                                                      ; FSpriteInfo.Height
                INC L                                                           ; FSprite выровнен по 8 байт

                ; чтение смещения по горизонтали
                LD A, (HL)                                                      ; FSpriteInfo.SOx
                INC L                                                           ; FSprite выровнен по 8 байт

                PUSH BC
                EXX
                POP BC

                ; -----------------------------------------
.HorizontClip   ; горизонтальное отсечение (начало)
                ; -----------------------------------------
                ; BC - размер спрайта (B - изначальная высота, С - ширина) в пикселях
                ; A  - смещение по горизонтали
                ; -----------------------------------------

                ; -----------------------------------------
                ; приведение к форме позиции 12.4 (по горизонтали)
                ; -----------------------------------------
                LD L, A
                ADD A, A
                SBC A, A
                LD H, A

                ; SOx <<= 4
                ADD HL, HL  ; x2
                ADD HL, HL  ; x4
                ADD HL, HL  ; x8
                ADD HL, HL  ; x16

                ; HL = SOx + Position.X
.PositionX      EQU $+1
                LD DE, #0000                                                    ; Position.X (позиция спрайта в мире)
                ADD HL, DE

                ; проверка достежения левой грани
                EX DE, HL
                LD A, (GameState.LeftEdge)
                LD L, A
                LD H, #00
                ADD HL, HL  ; x2
                ADD HL, HL  ; x4
                ADD HL, HL  ; x8
                ADD HL, HL  ; x16
                EX DE, HL

                SBC HL, DE
                JP M, .LeftClip                                                 ; переход, если спрайт лежит левее относительно левой грани

                ; -----------------------------------------
.RightClip      ; проверка положения спрайта относительно правой грани
                ; -----------------------------------------
                ; HL - положение спрайта (его левой грани) в пикселя, относительно видимой части (левой грани)
                ; BC - размер спрайта (B - изначальная высота, С - ширина) в пикселях
                ; -----------------------------------------

                ; преобразование позиции спрайта 12.4 к 8-битному (экранное значение)
                ADD HL, HL  ; x2
                LD E, H     ; начальная позиция в знакоместах
                ADD HL, HL  ; x4
                ADD HL, HL  ; x16
                ADD HL, HL  ; x32
                ; H - позиция спрайта по горизонтали в пикселях (левая грань спрайта)
                RET C                                                           ; выход, если спрайт находится правее относительно правой грани

                LD A, (GameState.VisibleWidth)                                  ; ширина видимой области (в знакоместах)
                LD L, A
                LD A, E
                SUB L
                CCF                                                             ; флаг неудачного определения bound спрайта
                RET C                                                           ; выход, если спрайт находится правее относительно правой грани
                ; --------------------------------------------------------------
                ; *вставка для отражающего спрайта
                ; смещение видимой области
                EX AF, AF'
                LD E, A                                                         ; аккумулятор хранит FSprite.Data.Page
                
                ; расчёт положения спрайта на экране в пикселях
                LD A, (GameState.LeftEdge)
                ADD A, H
                LD D, A                                                         ; положение спрайта на экране

                ; проверка флага, отражение спрайта
                BIT SPR_MIRROR_BIT, E
                JR Z, .SavePositionR                                            ; переход, если спрайт не отражённый

                ; спрайт является отражающим, но если он выровнен по знакоместу
                ; сместимся на 1 знакоместо раньше (необходимо для последующего корректно расчёта)
                AND #07
                LD A, D
                JR NZ, .SavePositionR                                           ; переход, если положение не выровнено по знакоместу

                ; сместимся на 1 знакоместо раньше (необходимо для последующего корректно расчёта)
                LD A, D
                SUB #08
.SavePositionR  EX AF, AF'
                ; --------------------------------------------------------------

                ; расчёт ширины невидимой части спрайта в пикселях
                LD A, L                                                         ; ширина видимой области (в знакоместах)
                ADD A, A    ; x2
                ADD A, A    ; x4
                ADD A, A    ; x8
                SUB H                                                           ; положение спрайта по горизонтали в пикселях
                CP C
                JR NC, .HorizontClipped                                         ; переход, если спрайт полностью помещается на экране
                LD C, A
                JR .HorizontClipped

.LeftClip       ; -----------------------------------------
                ; спрайта находится левее относительно левой грани
                ; -----------------------------------------
                ; HL - положение спрайта (его левой грани) в пикселя, относительно видимой части тайловой карты
                ; BC - размер спрайта (B - изначальная высота, С - ширина) в пикселях
                ; -----------------------------------------

                ; преобразование ширины спрайта к 16-битному
                LD D, #00
                LD A, C     ; xxxwwwww
                ADD A, A    ; xxwwwww0
                ADD A, A    ; xwwwww00
                ADD A, A    ; wwwww000 : w
                RL D        ; 0000000w wwwww000
                ADD A, A    ; wwww0000 : w
                RL D        ; 000000ww wwww0000
                LD E, A

                ADC HL, DE                                                      ; флаг С сброшен
                SCF                                                             ; флаг неудачного определения bound спрайта
                RET M                                                           ; выход, если спрайт находится левее относительно левой грани

                ; преобразование позиции спрайта 12.4 к 8-битному (экранное значение)
                ADD HL, HL  ; x2
                ADD HL, HL  ; x4
                ADD HL, HL  ; x16
                ADD HL, HL  ; x32
                ; H - позиция спрайта по горизонтали в пикселях (правая грань спрайта)

                LD A, H
                OR A
                CCF                                                             ; флаг неудачного определения bound спрайта
                RET Z                                                           ; выход, если спрайт находится левее относительно левой грани

                ; расчёт ширины невидимой части спрайта в пикселях
                LD C, H
                EX AF, AF'
                ; --------------------------------------------------------------
                ; *вставка для отражающего спрайта

                ; проверка флага, отражение спрайта
                ; аккумулятор хранит FSprite.Data.Page
                BIT SPR_MIRROR_BIT, A
                JR Z, .SetLeftEdge                                              ; переход, если спрайт не отражённый

                ; спрайт является отражающим, но если он выровнен по знакоместу
                ; сместимся на 1 знакоместо раньше (необходимо для последующего корректно расчёта)
                LD A, H
                AND #07
                JR NZ, .SetLeftEdge                                             ; переход, если положение не выровнено по знакоместу

                ; сместимся на 1 знакоместо раньше (необходимо для последующего корректно расчёта)
                LD A, (GameState.LeftEdge)
                SUB #08
                JR .SavePositionL

.SetLeftEdge    ; смещение видимой области
                LD A, (GameState.LeftEdge)
.SavePositionL  EX AF, AF'
                ; A' - позиция спрайта по горизонтали в пикселях

                ; -----------------------------------------
.HorizontClipped; горизонтальное отсечение (завершение)
                ; -----------------------------------------
                ; A' - позиция спрайта по горизонтали в пикселях
                ; BC - размер спрайта (B - изначальная высота, С - ширина изначальная/изменённая) в пикселях
                ; -----------------------------------------
                
                EXX
                EX AF, AF'
                LD E, A                                                         ; сохранение позиции спрайта по горизонтали в пикселях

                ; чтение смещения по вертикали
                LD A, (HL)                                                      ; FSpriteInfo.SOy
                EXX
                ; -----------------------------------------
.VerticalClip   ; вертекальное отсечение
                ; -----------------------------------------
                ; BC  - размер спрайта (B - изначальная высота, С - ширина изначальная/изменённая) в пикселях
                ; A  - смещение по вертикали
                ; -----------------------------------------

                ; -----------------------------------------
                ; преобразование к форме позиции 12.4 (по вертикали)
                ; -----------------------------------------
                LD L, A
                ADD A, A
                SBC A, A
                LD H, A

                ; SOy <<= 4
                ADD HL, HL  ; x2
                ADD HL, HL  ; x4
                ADD HL, HL  ; x8
                ADD HL, HL  ; x16

                ; HL = SOy + Position.Y
.PositionY      EQU $+1
                LD DE, #0000                                                    ; Position.Y (позиция спрайта в мире)
                ADD HL, DE

                ; проверка достежени верхней грани
                EX DE, HL
                LD A, (GameState.TopEdge)
                LD L, A
                LD H, #00
                ADD HL, HL  ; x2
                ADD HL, HL  ; x4
                ADD HL, HL  ; x8
                ADD HL, HL  ; x16
                EX DE, HL
                SBC HL, DE
                JP M, .TopClip                                                  ; переход, если спрайт лежит выше относительно верхней грани

                ; -----------------------------------------
.BottomClip     ; проверка положения спрайта относительно нижней грани
                ; -----------------------------------------
                ; HL - положение спрайта (его верхней грани) в пикселя, относительно видимой части тайловой карты
                ; BC - размер спрайта (B - изначальная высота, С - ширина) в пикселях
                ; -----------------------------------------

                ; преобразование позиции спрайта 12.4 к 8-битному (экранное значение)
                ADD HL, HL  ; x2
                ADD HL, HL  ; x4
                ADD HL, HL  ; x16
                ADD HL, HL  ; x32
                ; H - позиция спрайта по вертикали в пикселях (верхняя грань спрайта)
                RET C                                                           ; выход, если спрайт находится ниже относительно нижней грани

                LD A, (GameState.VisibleHeight)                                 ; высота видимой области SCR_WORLD_SIZE_Y << 3
                SUB H
                RET C                                                           ; выход, если спрайт находится ниже относительно нижней грани
                SCF                                                             ; флаг неудачного определения bound спрайта
                RET Z                                                           ; выход, если спрайт находится на нижней грани

                ; смещение видимой области
                EX AF, AF'
                LD A, (GameState.TopEdge)
                ADD A, H
                LD D, A
                EX AF, AF'
                
                ; расчёт высоты невидимой части спрайта в пикселях
                SUB B
                JR NC, .VerticalClipped                                         ; переход, если спрайт полностью помещается на экране

                ; корректировка высоты спрайта
                ADD A, B
                LD B, A

                ; D - позиция спрайта по вертикали в пикселях
                ; B - новая высота видимой части спрайта в пикселях (+)
                JR .VerticalClipped

                ; -----------------------------------------
.TopClip        ; спрайта находится выше относительно верхней грани
                ; -----------------------------------------
                ; HL - положение спрайта (его верхней грани) в пикселя, относительно видимой части тайловой карты
                ; BC - размер спрайта (B - изначальная высота, С - ширина) в пикселях
                ; -----------------------------------------

                ; преобразование высоты спрайта к 16-битному
                LD D, #00
                LD A, B     ; xxxwwwww
                ADD A, A    ; xxwwwww0
                ADD A, A    ; xwwwww00
                ADD A, A    ; wwwww000 : w
                RL D        ; 0000000w wwwww000
                ADD A, A    ; wwww0000 : w
                RL D        ; 000000ww wwww0000
                LD E, A

                ADC HL, DE                                                      ; флаг С сброшен
                SCF                                                             ; флаг неудачного определения bound спрайта
                RET M                                                           ; выход, если спрайт находится выше относительно верхней грани

                ; преобразование позиции спрайта 12.4 к 8-битному (экранное значение)
                ADD HL, HL  ; x2
                ADD HL, HL  ; x4
                ADD HL, HL  ; x16
                ADD HL, HL  ; x32
                ; H - позиция спрайта по вертикали в пикселях (нижняя грань спрайта)

                LD A, H
                OR A
                SCF                                                             ; флаг неудачного определения bound спрайта
                RET Z                                                           ; выход, если спрайт находится выше относительно верхней грани

                ; расчёт высоты невидимой части спрайта в пикселях
                LD L, A
                NEG
                ADD A, B
                LD B, L

                ; смещение видимой области
                LD A, (GameState.TopEdge)
                LD D, A

                ; A' - позиция спрайта по вертикали в пикселях
                ; B  - новая высота спрайта видимой части спрайта в пикселях

                ; -----------------------------------------
.VerticalClipped; вертикальное отсечение (завершение)
                ; -----------------------------------------
                EXX
                LD A, E
                EXX
                LD E, A

                OR A                                                            ; флаг успешности определения bound спрайта
                RET

                display " - Get sprite AABB:\t\t\t\t\t", /A, SpriteBound, "\t= busy [ ", /D, $ - SpriteBound, " byte(s)  ]"
                endmodule

                endif ; ~ _DRAW_UTILS_GET_SPRITE_AABB_
