                ifndef _DRAW_HEXAGON_COLUMN_NEW_
                define _DRAW_HEXAGON_COLUMN_NEW_
; -----------------------------------------
; точка входа отображение гексагонального столбца
; In:
;   SP - адрес следующей функции
;   DE - адрес экрана
;   BC - адрес спрайта
;   IX - адрес списка отображения
;   IY - адрес Adr.RenderBuffer + 80 (для сохранения бит высоты столбца)
; Out:
; Corrupt:
; Note:
;   отображение производится снизу вверх
; -----------------------------------------
EntryPointDraw: ; -----------------------------------------
                ; представление дисплейного списка
                ; +0 - первая рисуемая колонка первого гексагона (0-6)
                ; +1 - индекс/смещение в рендер буфере обрабатываемого гексагона
                ; +2 - номер строки по вертикали в пикселях
                ; +3 - количество пропускаемых знакомест (спрайт находится ниже экрана)
                ; -----------------------------------------

                ; проверка обновления столбца
                SRL (IY + 0)                                                    ; выталкивание флага, необходимости обновить столбец гексагона (0 - обновление столбца не требуется)
                JP NC, Column.New.NextColumn                                    ; переход, если обновление столбца не требуется
                
                LD (Column.ContainerSP), SP

                LD L, C
                LD H, B
                LD C, (HL)
                INC HL
                LD B, (HL)
                INC HL
                LD SP, HL

                ; DE  - адрес экрана
                LD L, D
                LD H, HIGH Adr.ScrAttrAdrTable
                LD H, (HL)
                LD L, E
                EX DE, HL

                ;   SP - адрес спрайта
                ;   HL - адрес экрана
                ;   DE - адрес атрибутов
                ;   BC - два батйта спрайта

                XOR A
                OR (IX + 3)
                RET Z                                                           ; вызов функции рисования, если нет пропуска знакомест
; -----------------------------------------
; пропуск знакоммест в гексагональном столбце
; In:
;   SP - адрес спрайта
;   HL - адрес экрана
;   DE - адрес атрибутов
;   BC - два батйта спрайта
;   IX - адрес списка отображения
;   IY - адрес Adr.RenderBuffer + 80 (для сохранения бит высоты столбца)
;   A  - количество пропускаемых знакомест (спрайт находится ниже экрана)
; Out:
; Corrupt:
; Note:
;   отображение производится снизу вверх
; -----------------------------------------
SkipBoundary:   ; в стеке хранится адрес функции рисования
                ; необходимо понять сколько пропускать данных, по адресу

                EX DE, HL                                                       ; сохраним адрес экрана в адресе атрибутов
                POP BC                                                          ; получение адреса функции
                LD HL, Column.New.x2
                OR A
                SBC HL, BC
                JR Z, .x2                        ; необходимо пропустить 2 байта

                LD HL, Column.New.x6
                OR A
                SBC HL, BC
                JR Z, .x6                        ; необходимо пропустить 6 байт

                ; пропуск 8 байт
                POP BC                                                          ; пропуск х2
.x6             POP BC                                                          ; пропуск х4
                POP BC                                                          ; пропуск x6
.x2             POP BC                                                          ; пропуск x8
                POP BC                                                          ; чтение два батйта атрибутов спрайта (текущего знакоместа)

                ; рассчёт адрес атрибутов повторно
                ; DE  - адрес экрана
                LD L, D
                LD H, HIGH Adr.ScrAttrAdrTable
                LD H, (HL)
                LD L, E
                EX DE, HL

                ATTR_MASK_SKIP                                                  ; пропус атрибутов знакоместа с маской
                SKIP_HEIGHT_BOUNDARY                                            ; сбор высоты знакоместа (нулевой высоты)

; -----------------------------------------
; отображение гексагонального столбца
; In:
;   SP - адрес следующей функции
;   DE - адрес экрана
;   BC - адрес спрайта
;   IX - адрес списка отображения
;   IY - адрес Adr.RenderBuffer + 80 (для сохранения бит высоты столбца)
; Out:
; Corrupt:
; Note:
;   отображение производится снизу вверх
; -----------------------------------------
Column.New:     ; зашить адрес функции  в спрайт?
;                 LD L, C
;                 LD H, B
;                 JP (HL)
;
;                 LD (.Jump), BC
;                 POP BC
; .Jump           EQU $+1
;                 JP #0000

.BoundaryLoop   CHECK_TOP_SCR                                                   ; проверка достижения верхней границы (в знакоместах)
.x8             _2_PIXELS_LINES                                                 ; отображение двух пиксельных строк знакоместа
.x6             _2_PIXELS_LINES                                                 ; отображение двух пиксельных строк знакоместа
                _2_PIXELS_LINES                                                 ; отображение двух пиксельных строк знакоместа
.x2             _2_PIXELS_LINES_                                                ; отображение двух пиксельных строк знакоместа (завершающий)
                ATTR_MASK                                                       ; отображение атрибутов знакоместа с маской
                STORE_HEIGHT_BOUNDARY                                           ; сбор высоты знакоместа
                SRA A                                                           ; флаг маски отвечает, отвечающий за последующее рисование
                                                                                ;   если флаг сброшен, следующиее идёт вывод без маски
                                                                                ;   если флаг установлен, следующее идёт вывод с маской
                JR NC, .BoundaryLoop                                            ; переход, если флаг рисования без маски

                ;   SP  - адрес спрайта
                ;   HL  - адрес экрана
                ;   DE  - адрес атрибутов
                ;   BC  - два батйта спрайта
                ;   A   - хранит высоту последующего вывода с маской

.MaskLoop       EX AF, AF'                                                      ; сохранение высоты последующего вывода с маской
                CHECK_TOP_SCR                                                   ; проверка достижения верхней границы (в знакоместах)
                ; -----------------------------------------
                ; вывод с маской
                ; -----------------------------------------
                PIXEL_UP_HL                                                     ; переход на знакоместо выше адрес адрес пикселей (в знакоместах)
                ATTR_UP_DE                                                      ; переход на строку выше адрес атрибутов

.InViewMask     LD A, E
                EX AF, AF'
                CP #08
                JR NC, .Full                                                    ; переход, если рисуется всё знакоместо

                ; ToDo хранить высоту вывода отрицательным
                ; расчёт смещения для перехода
                LD E, A
                XOR A
                SUB E
                AND #07
                LD E, A
                ADD A, A    ; x2
                ADD A, E    ; x3
                ADD A, A    ; x6
                LD (.MaskJump), A

                EX AF, AF'
                LD E, A

.MaskJump       EQU $+1
                JR $
.Full           PIXEL_MASK
                PIXEL_MASK
                PIXEL_MASK
                PIXEL_MASK
                PIXEL_MASK
                PIXEL_MASK
                PIXEL_MASK
                PIXEL_MASK_
                ATTR_MASK                                                       ; отображение атрибутов знакоместа с маской
                STORE_HEIGHT_BOUNDARY_1                                         ; сохранение данных высоты (всегда высокий) в рендер буфере текущего столбца

                ; проверка окончания данных спрайта
                OR A
                JR NZ, .MaskLoop                                                ; переход, если незакончились данные спрайта

.SkipBoundary   ; пропуск незадействованых знакомест
                LD A, #04
                SUB C
                ADD A, A    ; x2
                ADD A, A    ; x4
                LD (.FillJump), A
.FillJump       EQU $+1
                JR $
                STORE_HEIGHT_BOUNDARY_0
                STORE_HEIGHT_BOUNDARY_0
                STORE_HEIGHT_BOUNDARY_0
                STORE_HEIGHT_BOUNDARY_0

.ContainerSP    EQU $+1
.Exit           LD SP, #0000

.NextColumn     ; стартовый адрес таблицы смещений по горизонтали
.OffsetTable    EQU $+1
                LD HL, #0000
                
                EXX
                INC E
                PUSH DE                                                         ; сохранение, адреса строки экрана
                EXX

                ; чтение адреса спрайта
                LD C, (HL)
                INC HL
                LD B, (HL)
                INC HL
                LD (.OffsetTable), HL

                POP DE                                                          ; восстановить адрес экрана
                INC IYL                                                         ; следующая колонка
                RET

                display " - Draw hexagon column new:\t\t\t\t", /A, SkipBoundary, "\t= busy [ ", /D, $-SkipBoundary, " byte(s)  ]"

                endif ; ~ _DRAW_HEXAGON_COLUMN_NEW_
