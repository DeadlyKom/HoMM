                ifndef _DRAW_HEXAGON_COLUMN_NEW_
                define _DRAW_HEXAGON_COLUMN_NEW_
; -----------------------------------------
; отображение двух пиксельных строк знакоместа
; In:
;   SP  - адрес спрайта
;   HL  - адрес экрана
;   DE  - адрес атрибутов
;   BC  - два батйта спрайта
; Out:
; Corrupt:
; Note:
;   отображение производится снизу вверх
; -----------------------------------------
_2_PIXELS_LINES_N macro
                POP BC
                LD (HL), C
                DEC H
                LD (HL), B
                DEC H
                endm
; -----------------------------------------
; отображение двух пиксельных строк знакоместа (завершающий)
; In:
;   SP  - адрес спрайта
;   HL  - адрес экрана
;   DE  - адрес атрибутов
;   BC  - два батйта спрайта
; Out:
; Corrupt:
; Note:
;   отображение производится снизу вверх
; -----------------------------------------
_2_PIXELS_LINES__N macro
                POP BC
                LD (HL), C
                DEC H
                LD (HL), B
                endm
; -----------------------------------------
; отображение пиксельной строки знакоместа с маской
; In:
;   SP  - адрес спрайта
;   HL  - адрес экрана
;   DE  - адрес атрибутов
;   BC  - два батйта спрайта (значение маски и спрайта)
; Out:
; Corrupt:
; Note:
;   отображение производится снизу вверх
; -----------------------------------------
PIXEL_MASK_N      macro
                POP BC
                LD A, (HL)  ; чтение байта из экрана
                OR C        ; маска
                XOR B       ; спрайт
                LD (HL), A  ; запись байта в экран
                DEC H       ; следующая строка
                endm
PIXEL_MASK__N    macro
                POP BC
                LD A, (HL)  ; чтение байта из экрана
                OR C        ; маска
                XOR B       ; спрайт
                LD (HL), A  ; запись байта в экран
                endm
; -----------------------------------------
; проверка достижения верхней границы (в знакоместах)
; In:
;   SP  - адрес спрайта
;   HL  - адрес экрана
;   DE  - адрес атрибутов
; Out:
; Corrupt:
; Note:
;   отображение производится снизу вверх
; -----------------------------------------
CHECK_TOP_SCR_N   macro
                ; проверка нахождения в вверхнем сегменте
                LD A, D
                AND %00000011
                JR NZ, $+10                                                     ; переход, если 1ый или 2ой сегмент

                ; проверка верхней границы в нулевом сегменте
                LD A, E
                AND %11100000
                SUB #20
                JP Z, Column.New.Exit                                               ; выход, достигли верхней границы
                endm
; -----------------------------------------
; отображение атрибутов знакоместа с маской
; In:
;   SP  - адрес спрайта
;   HL  - адрес экрана
;   DE  - адрес атрибутов
;   BC  - два батйта атрибутов (значение маски и спрайта)
; Out:
;   A   - метаданные знакоместа
;
;          7    6    5    4    3    2    1    0
;       +----+----+----+----+----+----+----+----+
;       | D5 | D4 | D3 | D2 | D1 | D0 | DM | HG |
;       +----+----+----+----+----+----+----+----+
;
;       HG      [0]         - флаг, высота знакоместа, учитывается при наложении тумана
;                               0 - низкое знакоместо, обвалакивается туманом
;                               1 - высокое знакоместо, туман не рисуется
;       DM      [1]         - флаг,  отвечает, за вывод последующее знакоместа
;                               0 - вывод без маски
;                               1 - вывод с маской
;       D5-D0   [2-0]       - высоту последующего вывода с маской
;                             нулевая высота, говорит о завершении отрисовкаи спрайта с маской,
;                             либо высота меньше 8 и ранний выход
;      ! ВАЖНО !
;      если идут данные вывода с маской 0 и 1 биты отсутствуют, байт хранит высоту вывода с маской
;      нулевая высота, либо высота меньше 8, говорит о завершении отрисовкаи спрайта с маской
; Corrupt:
; Note:
;   отображение производится снизу вверх
; -----------------------------------------
ATTR_MASK_N       macro
                POP BC                                                          ; чтение два батйта атрибутов спрайта (текущего знакоместа)
                LD A, C
                ADD A, A                                                        ; << 1 
                                                                                ; 7ой бит после сдвига отвечающий за мерцание знакоместа,
                                                                                ; должен быть сброшенным.
                                                                                ; т.к. ink всегда чёрный, то сдвиг влево корректно инициализирует цвет
                JR NC, $+3
                LD (DE), A  ; запись байта в экран атрибутов
                LD A, B
                endm
; -----------------------------------------
; пропус атрибутов знакоместа с маской
; In:
;   SP  - адрес спрайта
;   HL  - адрес экрана
;   DE  - адрес атрибутов
;   BC  - два батйта атрибутов (значение маски и спрайта)
; Out:
;   A   - метаданные знакоместа
;
;          7    6    5    4    3    2    1    0
;       +----+----+----+----+----+----+----+----+
;       | D5 | D4 | D3 | D2 | D1 | D0 | DM | HG |
;       +----+----+----+----+----+----+----+----+
;
;       HG      [0]         - флаг, высота знакоместа, учитывается при наложении тумана
;                               0 - низкое знакоместо, обвалакивается туманом
;                               1 - высокое знакоместо, туман не рисуется
;       DM      [1]         - флаг,  отвечает, за вывод последующее знакоместа
;                               0 - вывод без маски
;                               1 - вывод с маской
;       D5-D0   [2-0]       - высоту последующего вывода с маской
;                             нулевая высота, говорит о завершении отрисовкаи спрайта с маской,
;                             либо высота меньше 8 и ранний выход
;      ! ВАЖНО !
;      если идут данные вывода с маской 0 и 1 биты отсутствуют, байт хранит высоту вывода с маской
;      нулевая высота, либо высота меньше 8, говорит о завершении отрисовкаи спрайта с маской
; Corrupt:
; Note:
;   имитация отображения производится снизу вверх
; -----------------------------------------
ATTR_MASK_SKIP_N macro
                POP BC                                                          ; чтение два батйта атрибутов спрайта (текущего знакоместа)
                LD A, B
                endm
; -----------------------------------------
; сбор высоты знакоместа
; In:
;   SP - адрес спрайта
;   HL - адрес экрана
;   BC - два батйта спрайта (значение маски и спрайта)
;   IX - адрес Adr.RenderBuffer + 64 (для сохранения бит высоты столбца)
;   IY - адрес Adr.RenderBuffer (хранит информацио о выводе)
;
;   структура хранения данных Adr.RenderBuffer:
;    * для хранения информации о гексагональном тайле требуется 7 байт, где
;      - байт в Adr.RenderBuffer'е по индексу хранит информацио о выводе
;
;          7    6    5    4    3    2    1    0
;       +----+----+----+----+----+----+----+----+
;       | HU | FG | .. | .. | .. | A2 | A1 | A0 |
;       +----+----+----+----+----+----+----+----+
;   
;       HU      [7]         - флаг, принудительного обновления гексагона (частично или полностью)
;       FG      [6]         - флаг, тумана (0 - гексагон закрашен туманом целиком)
;       ⚠️ два флага HU и FG позволяют управлять процесом смены видимого гексагона на невидимый ⚠️
;       A2-A0   [2-0]       - номер анимации гексагона (локальный)
;
;      - 1-6 байт по байту на каждый столбцов гексагона, хранят биты высоты столбца (бит на знакоместо высоты)
;        адрес = Adr.RenderBuffer + 64 + номер строки * 22 + номер знакоместа (0-4) * 6 + номер столбца (0-5)
;
;       ℹ️ структура байта в начале рисования столбца
;          7    6    5    4    3    2    1    0
;       +----+----+----+----+----+----+----+----+
;       | 0  | 0  | 0  | 0  | 0  | 0  | 0  | CU |
;       +----+----+----+----+----+----+----+----+
;
;       CU      [0]         - флаг, необходимости обновить столбец гексагона (0 - обновление столбца не требуется)
;
;       ℹ️ структура байта в конце, если столбец обновлён
;          7    6    5    4    3    2    1    0
;       +----+----+----+----+----+----+----+----+
;       | B7 | B6 | B5 | B4 | B3 | B2 | B1 | B0 |
;       +----+----+----+----+----+----+----+----+
;
;       B7-B0   [7-0]       - последовательность битов снизу вверх,
;                             информация высоты гексагона
;
;   A   - метаданные знакоместа
;
;          7    6    5    4    3    2    1    0
;       +----+----+----+----+----+----+----+----+
;       | D5 | D4 | D3 | D2 | D1 | D0 | DM | HG |
;       +----+----+----+----+----+----+----+----+
;
;       HG      [0]         - флаг, высота знакоместа, учитывается при наложении тумана
;                               0 - низкое знакоместо, обвалакивается туманом
;                               1 - высокое знакоместо, туман не рисуется
;       DM      [1]         - флаг,  отвечает, за вывод последующее знакоместа
;                               0 - вывод без маски
;                               1 - вывод с маской
;       D5-D0   [2-0]       - высоту последующего вывода с маской
;
;      ⚠️ ВАЖНО ⚠️
;      если идут данные вывода с маской 0 и 1 биты отсутствуют, байт хранит высоту вывода с маской
;      нулевая высота, либо высота меньше 8, говорит о завершении отрисовкаи спрайта с маской
; -----------------------------------------
STORE_HEIGHT_BOUNDARY_N macro
                ; сохранение высоты знакоместа
                SRL A                                                           ; выталкивание флага, высота знакоместа с сохранением значений в аккумуляторе
                RL (IY + 0)                                                     ; сохранение данных высоты в рендер буфере текущего столбца
                endm
STORE_HEIGHT_BOUNDARY_0_N macro
                ; сохранение высоты знакоместа
                SLA (IY + 0)                                                    ; сохранение данных высоты (всегда низкий) в рендер буфере текущего столбца
                endm
STORE_HEIGHT_BOUNDARY_1_N macro
                ; сохранение высоты знакоместа
                SLL (IY + 0)                                                    ; сохранение данных высоты (всегда высокий) в рендер буфере текущего столбца
                endm
; сбор высоты знакоместа (пропуск)
SKIP_HEIGHT_BOUNDARY_N macro
                SRL A                                                           ; выталкивание флага (не требуется)
                SLA (IY + 0)                                                    ; сохранение нулевой высоты в рендер буфере текущего столбца
                endm
; проверка обновления столбца
CHECK_UPDATE_COUMN_N macro
                SRL (IY + 0)                                                    ; выталкивание флага, необходимости обновить столбец гексагона (0 - обновление столбца не требуется)
                SCF     ; временно (принудительно рисовать)
                JP NC, Column.NextColumn                                        ; переход, если обновление столбца не требуется
                endm
; -----------------------------------------
; точка входа отображение гексагонального столбца
; In:
;   SP - адрес следующей функции
;   DE - адрес экрана
;   BC - адрес спрайта
;   IX - адрес списка отображения
;   IY - адрес Adr.RenderBuffer + 80 (для сохранения бит высоты столбца)
; Out:
; Corrupt:
; Note:
;   отображение производится снизу вверх
; -----------------------------------------
EntryPointDraw: ; -----------------------------------------
                ; представление дисплейного списка
                ; +0 - первая рисуемая колонка первого гексагона (0-6)
                ; +1 - индекс/смещение в рендер буфере обрабатываемого гексагона
                ; +2 - номер строки по вертикали в пикселях
                ; +3 - количество пропускаемых знакомест (спрайт находится ниже экрана)
                ; -----------------------------------------

                CHECK_UPDATE_COUMN_N                                            ; проверка обновления столбца
                LD (Column.New.ContainerSP), SP

                LD L, C
                LD H, B

                ; чтение адреса функции
                LD C, (HL)
                INC HL
                LD B, (HL)
                INC HL
                LD SP, HL

                ; DE  - адрес экрана
                LD L, D
                LD H, HIGH Adr.ScrAttrAdrTable
                LD H, (HL)
                LD L, E
                EX DE, HL

                ;   SP - адрес спрайта
                ;   HL - адрес экрана
                ;   DE - адрес атрибутов
                ;   BC - два батйта спрайта

                XOR A
                OR (IX + 3)
                RET Z                                                           ; вызов функции рисования, если нет пропуска знакомест
; -----------------------------------------
; пропуск знакоммест в гексагональном столбце
; In:
;   SP - адрес спрайта
;   HL - адрес экрана
;   DE - адрес атрибутов
;   BC - два батйта спрайта
;   IX - адрес списка отображения
;   IY - адрес Adr.RenderBuffer + 80 (для сохранения бит высоты столбца)
;   A  - количество пропускаемых знакомест (спрайт находится ниже экрана)
; Out:
; Corrupt:
; Note:
;   отображение производится снизу вверх
; -----------------------------------------
SkipBoundary:   ; в стеке хранится адрес функции рисования
                ; необходимо понять сколько пропускать данных, по адресу

                EX AF, AF'                                                      ; сохранение количество пропускаемых знакомест
                EX DE, HL                                                       ; сохраним адрес экрана в адресе атрибутов
                POP BC                                                          ; получение адреса функции
                LD HL, Column.New.x2_
                OR A
                SBC HL, BC
                JR Z, .x2                        ; необходимо пропустить 2 байта

                LD HL, Column.New.x6_
                OR A
                SBC HL, BC
                JR Z, .x6                        ; необходимо пропустить 6 байт

                ; пропуск 8 байт
                POP BC                                                          ; пропуск х2
.x6             POP BC                                                          ; пропуск х4
                POP BC                                                          ; пропуск x6
.x2             POP BC                                                          ; пропуск x8
                ATTR_MASK_SKIP_N                                                  ; пропус атрибутов знакоместа с маской
                SKIP_HEIGHT_BOUNDARY_N                                            ; сбор высоты знакоместа (нулевой высоты)

                ; рассчёт адрес атрибутов повторно
                ; DE  - адрес экрана
                LD L, D
                LD H, HIGH Adr.ScrAttrAdrTable
                LD H, (HL)
                LD L, E
                EX DE, HL

.BoundarySkip   EX AF, AF'                                                      ; восстановление количество пропускаемых знакомест
                DEC A
                JR Z, Column.New.x8                                             ; переход, если последующее отображение колонки в видимой области
                EX AF, AF'                                                      ; сохранение количество пропускаемых знакомест

                ; BC хранит 2 байта спрайта (текущего знакоместа)
                POP BC                                                          ; пропуск х2
                POP BC                                                          ; пропуск х4
                POP BC                                                          ; пропуск x6
                POP BC                                                          ; пропуск x8
                ATTR_MASK_SKIP_N                                                  ; пропус атрибутов знакоместа с маской
                SKIP_HEIGHT_BOUNDARY_N                                            ; сбор высоты знакоместа (нулевой высоты)
                SRA A                                                           ; флаг маски отвечает, отвечающий за последующее рисование
                                                                                ;   если флаг сброшен, следующиее идёт вывод без маски
                                                                                ;   если флаг установлен, следующее идёт вывод с маской
                JR NC, .BoundarySkip                                            ; переход, если флаг рисования без маски
                                                                                ; без проверки верхней границы видимой области
.MaskSkipLoop   ; -----------------------------------------
                ; пропуск с маской
                ; -----------------------------------------                                                        
                ; ToDo хранить высоту вывода отрицательным
                EX AF, AF'                                                      ; восстановление данных пропускаемых знакомест (значение равно нулю)
                JP Z, Column.New.InViewMask                                               ; переход, если последующее отображение колонки в видимой области
                DEC A
                EX AF, AF'

                NEG
                ADD A, #08
                LD (.SkipJump), A
.SkipJump       EQU $+1
                JR $
                POP BC
                POP BC
                POP BC
                POP BC
                POP BC
                POP BC
                POP BC
                POP BC
                ATTR_MASK_SKIP_N                                                  ; пропус атрибутов знакоместа с маской
                STORE_HEIGHT_BOUNDARY_0_N                                         ; сохранение данных высоты (всегда низкий) в рендер буфере текущего столбца

                ; проверка окончания данных спрайта
                OR A
                JP Z, Column.New.SkipBoundary                                             ; переход, если закончились данные спрайта
                JR .MaskSkipLoop
; -----------------------------------------
; отображение гексагонального столбца
; In:
;   SP - адрес следующей функции
;   DE - адрес экрана
;   BC - адрес спрайта
;   IX - адрес списка отображения
;   IY - адрес Adr.RenderBuffer + 80 (для сохранения бит высоты столбца)
; Out:
; Corrupt:
; Note:
;   отображение производится снизу вверх
; -----------------------------------------
Column.New:     ; корректировка начального адреса экрана (в знакоместе)
.x2_            RES 1, H
                RES 2, H
                JR .x2
.x6_            RES 1, H
                JR .x6

.BoundaryLoop   CHECK_TOP_SCR_N                                                   ; проверка достижения верхней границы (в знакоместах)
                PIXEL_UP_HL                                                     ; переход на знакоместо выше адрес адрес пикселей (в знакоместах)
                ATTR_UP_DE                                                      ; переход на строку выше адрес атрибутов
.x8             _2_PIXELS_LINES_N                                                 ; отображение двух пиксельных строк знакоместа
.x6             _2_PIXELS_LINES_N                                                 ; отображение двух пиксельных строк знакоместа
                _2_PIXELS_LINES_N                                                 ; отображение двух пиксельных строк знакоместа
.x2             _2_PIXELS_LINES__N                                                ; отображение двух пиксельных строк знакоместа (завершающий)
                ATTR_MASK_N                                                       ; отображение атрибутов знакоместа с маской
                STORE_HEIGHT_BOUNDARY_N                                           ; сбор высоты знакоместа
                SRA A                                                           ; флаг маски отвечает, отвечающий за последующее рисование
                                                                                ;   если флаг сброшен, следующиее идёт вывод без маски
                                                                                ;   если флаг установлен, следующее идёт вывод с маской
                JR NC, .BoundaryLoop                                            ; переход, если флаг рисования без маски

                ;   SP  - адрес спрайта
                ;   HL  - адрес экрана
                ;   DE  - адрес атрибутов
                ;   BC  - два батйта спрайта
                ;   A   - хранит высоту последующего вывода с маской

.MaskLoop       EX AF, AF'                                                      ; сохранение высоты последующего вывода с маской
                CHECK_TOP_SCR_N                                                   ; проверка достижения верхней границы (в знакоместах)
                ; -----------------------------------------
                ; вывод с маской
                ; -----------------------------------------
                PIXEL_UP_HL                                                     ; переход на знакоместо выше адрес адрес пикселей (в знакоместах)
                ATTR_UP_DE                                                      ; переход на строку выше адрес атрибутов

.InViewMask     LD A, E
                EX AF, AF'
                CP #08
                JR NC, .Full                                                    ; переход, если рисуется всё знакоместо

                ; ToDo хранить высоту вывода отрицательным
                ; расчёт смещения для перехода
                LD E, A
                XOR A
                SUB E
                AND #07
                LD E, A
                ADD A, A    ; x2
                ADD A, E    ; x3
                ADD A, A    ; x6
                LD (.MaskJump), A

                EX AF, AF'
                LD E, A

.MaskJump       EQU $+1
                JR $
.Full           PIXEL_MASK_N
                PIXEL_MASK_N
                PIXEL_MASK_N
                PIXEL_MASK_N
                PIXEL_MASK_N
                PIXEL_MASK_N
                PIXEL_MASK_N
                PIXEL_MASK__N
                ATTR_MASK_N                                                       ; отображение атрибутов знакоместа с маской
                STORE_HEIGHT_BOUNDARY_1_N                                         ; сохранение данных высоты (всегда высокий) в рендер буфере текущего столбца

                ; проверка окончания данных спрайта
                OR A
                JR NZ, .MaskLoop                                                ; переход, если незакончились данные спрайта

.SkipBoundary   ; пропуск незадействованых знакомест
                POP BC
                LD A, #04
                SUB C
                ADD A, A    ; x2
                ADD A, A    ; x4
                LD (.FillJump), A
.FillJump       EQU $+1
                JR $
                STORE_HEIGHT_BOUNDARY_0_N
                STORE_HEIGHT_BOUNDARY_0_N
                STORE_HEIGHT_BOUNDARY_0_N
                STORE_HEIGHT_BOUNDARY_0_N

.ContainerSP    EQU $+1
.Exit           LD SP, #0000

.NextColumn     ; стартовый адрес таблицы смещений по горизонтали
.OffsetTable    EQU $+1
                LD HL, #0000
                
                EXX
                INC C                                                           ; увеличить счётчик отрисованных колонок гексангона
                INC E
                PUSH DE                                                         ; сохранение, адреса строки экрана
                EXX

                ; чтение адреса спрайта
                LD C, (HL)
                INC HL
                LD B, (HL)
                INC HL
                LD (.OffsetTable), HL

                POP DE                                                          ; восстановить адрес экрана
                INC IYL                                                         ; следующая колонка
                RET

                display " - Draw hexagon column new:\t\t\t\t", /A, SkipBoundary, "\t= busy [ ", /D, $-SkipBoundary, " byte(s)  ]"

                endif ; ~ _DRAW_HEXAGON_COLUMN_NEW_
