                ifndef _DRAW_HEXAGON_COLUMN_
                define _DRAW_HEXAGON_COLUMN_
; -----------------------------------------
; отображение двух пиксельных строк знакоместа
; In:
;   SP  - адрес спрайта
;   HL  - адрес экрана
;   DE  - адрес атрибутов
;   BC  - два батйта спрайта
; Out:
; Corrupt:
; Note:
;   отображение производится снизу вверх
; -----------------------------------------
_2_PIXELS_LINES macro
                LD (HL), C
                DEC H
                LD (HL), B
                DEC H
                POP BC
                endm
; -----------------------------------------
; отображение двух пиксельных строк знакоместа (завершающий)
; In:
;   SP  - адрес спрайта
;   HL  - адрес экрана
;   DE  - адрес атрибутов
;   BC  - два батйта спрайта
; Out:
; Corrupt:
; Note:
;   отображение производится снизу вверх
; -----------------------------------------
_2_PIXELS_LINES_ macro
                LD (HL), C
                DEC H
                LD (HL), B
                ; DEC H
                POP BC
                endm
; -----------------------------------------
; отображение пиксельной строки знакоместа с маской
; In:
;   SP  - адрес спрайта
;   HL  - адрес экрана
;   DE  - адрес атрибутов
;   BC  - два батйта спрайта (значение маски и спрайта)
; Out:
; Corrupt:
; Note:
;   отображение производится снизу вверх
; -----------------------------------------
PIXEL_MASK      macro
                LD A, (HL)  ; чтение байта из экрана
                OR C        ; маска
                XOR B       ; спрайт
                LD (HL), A  ; запись байта в экран
                DEC H       ; следующая строка
                POP BC
                endm
PIXEL_MASK_      macro
                LD A, (HL)  ; чтение байта из экрана
                OR C        ; маска
                XOR B       ; спрайт
                LD (HL), A  ; запись байта в экран
                POP BC
                endm
; -----------------------------------------
; отображение атрибутов знакоместа с маской
; In:
;   SP  - адрес спрайта
;   HL  - адрес экрана
;   DE  - адрес атрибутов
;   BC  - два батйта атрибутов (значение маски и спрайта)
; Out:
;   A   - метаданные знакоместа
;
;          7    6    5    4    3    2    1    0
;       +----+----+----+----+----+----+----+----+
;       | D5 | D4 | D3 | D2 | D1 | D0 | DM | HG |
;       +----+----+----+----+----+----+----+----+
;
;       HG      [0]         - флаг, высота знакоместа, учитывается при наложении тумана
;                               0 - низкое знакоместо, обвалакивается туманом
;                               1 - высокое знакоместо, туман не рисуется
;       DM      [1]         - флаг,  отвечает, за вывод последующее знакоместа
;                               0 - вывод без маски
;                               1 - вывод с маской
;       D5-D0   [2-0]       - высоту последующего вывода с маской
;                             нулевая высота, говорит о завершении отрисовкаи спрайта с маской,
;                             либо высота меньше 8 и ранний выход
;      ! ВАЖНО !
;      если идут данные вывода с маской 0 и 1 биты отсутствуют, байт хранит высоту вывода с маской
;      нулевая высота, либо высота меньше 8, говорит о завершении отрисовкаи спрайта с маской
; Corrupt:
; Note:
;   отображение производится снизу вверх
; -----------------------------------------
ATTR_MASK       macro
                LD A, C
                ADD A, A                                                        ; << 1 
                                                                                ; 7ой бит после сдвига отвечающий за мерцание знакоместа,
                                                                                ; должен быть сброшенным.
                                                                                ; т.к. ink всегда чёрный, то сдвиг влево корректно инициализирует цвет
                JR NC, $+3
                LD (DE), A  ; запись байта в экран атрибутов
                LD A, B
                POP BC
                endm
; -----------------------------------------
; преобразование адрес экрана в атрибутный
; In:
;   SP  - адрес спрайта
;   HL  - адрес экрана
;   BC  - два батйта спрайта (значение маски и спрайта)
; Out:
;   DE  - адрес знакоместа атрибутов
; Corrupt:
;   DE
; Note:
;   отображение производится снизу вверх
; -----------------------------------------
TO_ATTR_ADR     macro
                LD E, L
                LD L, H
                LD H, HIGH Adr.ScrAttrAdrTable
                LD D, (HL)
                LD H, L
                LD L, E
                ; EX DE, HL
                endm
; -----------------------------------------
; переход на строку выше адрес атрибутов
; In:
;   SP  - адрес спрайта
;   HL  - адрес экрана
;   DE  - адрес знакоместа атрибутов
;   BC  - два батйта спрайта (значение маски и спрайта)
; Out:
;   DE  - адрес знакоместа атрибутов на строку выше
; Corrupt:
;   DE
; Note:
;   отображение производится снизу вверх
; -----------------------------------------
ATTR_UP_DE      macro
                LD A, E
                SUB #20
                LD E, A
                SBC A, A
                ADD A, D
                LD D, A
                endm
; -----------------------------------------
; переход на знакоместо выше адрес пикселей (в знакоместах)
; In:
;   SP  - адрес спрайта
;   HL  - адрес экрана
; Out:
; Corrupt:
; Note:
;   отображение производится снизу вверх
; -----------------------------------------
PIXEL_UP_HL     macro
                DEC H
                LD A, L
                ADD A, #E0
                LD L, A
                SBC A, A
                AND #08
                ADD A, H
                LD H, A
                endm
; -----------------------------------------
; проверка достижения верхней границы (в знакоместах)
; In:
;   SP  - адрес спрайта
;   HL  - адрес экрана
; Out:
; Corrupt:
; Note:
;   отображение производится снизу вверх
; -----------------------------------------
CHECK_TOP_SCR   macro
                ; проверка нахождения в вверхнем сегменте
                LD A, D
                AND %00000011
                JR NZ, $+10                                                     ; переход, если 1ый или 2ой сегмент

                ; проверка верхней границы в нулевом сегменте
                LD A, E
                AND %11100000
                SUB #20
                JP Z, Column.Exit                                               ; выход, достигли верхней границы
                endm
; -----------------------------------------
; обволакивание туманом
; In:
;   SP  - адрес спрайта
;   HL  - адрес экрана
;   BC  - два батйта спрайта (значение маски и спрайта)
;   HL' - адрес на рендер тайла-столбца(заволакивание тайла по знакоместам)
;   DE' - адрес спрайта тумана
; Out:
; Corrupt:
; Note:
;   отображение производится снизу вверх
; -----------------------------------------
FOG             macro
                ;
                ; LD A, B
                ; ADD A, A
                SRA A
                JR NC, .L3  ; переход, если знакоместо высокое (нельзя закрыть туманом)

                ; закрытие туманом
                LD (.L2), SP
                LD SP, #0000

                PUSH HL
                EXX

                ; HL - указывает на рендер тайла (заволакивание тайла по знакоместам)
                ; DE - на спрайт тумана

                RR (HL)
                JR NC, .L1  ; переход, если туман не заволакивает данное зннакоместо

                EX (SP), HL
                EX DE, HL

                LD C, (HL)
                INC HL
                LD B, (HL)
                INC HL
                LD (.L4), SP
                LD SP, HL

                EX DE, HL
                dup 3
                LD (HL), C
                INC H
                LD (HL), B
                INC H
                POP BC
                edup
                LD (HL), C
                INC H
                LD (HL), B

                LD HL, #0000
                ADD HL, SP
                EX DE, HL

.L4             EQU $+1
                LD SP, #0000
                EX (SP), HL

.L1             EXX
                POP AF

.L2             EQU $+1
                LD SP, #0000
.L3             ; ~туман
                endm

; сбор высоты знакоместа
; In:
;   SP  - адрес спрайта
;   HL  - адрес экрана
;   BC  - два батйта спрайта (значение маски и спрайта)
;   HL' - адрес Adr.RenderBuffer
;
;   структура хранения данных Adr.RenderBuffer:
;    * для хранения информации о гексагональном тайле требуется 7 байт, где
;      - 1ый байт хранит информацио о выводе
;
;          7    6    5    4    3    2    1    0
;       +----+----+----+----+----+----+----+----+
;       | HU | TV | .. | .. | .. | A2 | A1 | A0 |
;       +----+----+----+----+----+----+----+----+
;     
;       HU      [7]         - флаг, обновления гексагона (целиком или частично)
;       TV      [6]         - флаг, видимости гексагона (целиком)
;       A2-A0   [2-0]       - номер анимации тайла (локальный)
;
;      - последующие (6 байт по байту на каждый столбцов гексагона) хранят биты высоты столбца (бит на знакоместо высоты)
;
;          7    6    5    4    3    2    1    0
;       +----+----+----+----+----+----+----+----+
;       | .. | .. | B4 | B3 | B2 | B1 | B0 | CU |
;       +----+----+----+----+----+----+----+----+
;
;       CU      [0]         - флаг, обновления столбца гексагона (0 - обновление столбца не требуется)
;       B4-B0   [5-1]       - последовательность битов снизу вверх,
;                             информация высоты гексагона первые 5 знакомест
;                             оставшиеся 3 всегда высоки, если имеются
;
;   A   - метаданные знакоместа
;
;          7    6    5    4    3    2    1    0
;       +----+----+----+----+----+----+----+----+
;       | D5 | D4 | D3 | D2 | D1 | D0 | DM | HG |
;       +----+----+----+----+----+----+----+----+
;
;       HG      [0]         - флаг, высота знакоместа, учитывается при наложении тумана
;                               0 - низкое знакоместо, обвалакивается туманом
;                               1 - высокое знакоместо, туман не рисуется
;       DM      [1]         - флаг,  отвечает, за вывод последующее знакоместа
;                               0 - вывод без маски
;                               1 - вывод с маской
;       D5-D0   [2-0]       - высоту последующего вывода с маской
;
;      ! ВАЖНО !
;      если идут данные вывода с маской 0 и 1 биты отсутствуют, байт хранит высоту вывода с маской
;      нулевая высота, либо высота меньше 8, говорит о завершении отрисовкаи спрайта с маской

STORE_HEIGHT_BOUNDARY macro
                ; необходимо чтения/записи в HL'
                ; при переходе, регистр BC' будет хранит муссор,
                ; необходимо сместить стек со спрайта
                LD (.ContainerSP), SP
                LD SP, TemplateSP                                               ; временный буфер на два значения

                ; сохранение высоты знакоместа
                EXX
                SRL A                                                           ; выталкивание флага, высота знакоместа с сохранением значений в аккумуляторе
                SRL (HL)                                                        ; сохранение данных высоты в рендер буфере текущего столбца
                EXX

.ContainerSP    EQU $+1
                LD SP, #0000
                endm
; проверка обновления столбца
CHECK_UPDATE_COUMN macro
                ; стек указывает на временный массив адресов функций
                EXX
                INC L                                                           ; переход к следующему столбцу
                SRL (HL)                                                        ; выталкивание флага, обновления столбца гексагона
                SCF ; временно (принудительно рисовать)
                EXX
                JP NC, Column.NextColumn                                        ; переход, если обновление столбца не требуется
                endm
; -----------------------------------------
; отображение гексагонального столбца
; In:
;   SP  - адрес следующей функции
;   DE  - адрес экрана
;   BC  - адрес спрайта
; Out:
; Corrupt:
; Note:
;   отображение производится снизу вверх
; -----------------------------------------
Column:         
.x6             CHECK_UPDATE_COUMN                                              ; проверка обновления столбца

                RES 1, D

                LD (Column.ContainerSP), SP

                LD L, C
                LD H, B
                LD C, (HL)
                INC HL
                LD B, (HL)
                INC HL
                LD SP, HL

                ; DE  - адрес экрана
                LD L, D
                LD H, HIGH Adr.ScrAttrAdrTable
                LD H, (HL)
                LD L, E
                EX DE, HL

                ;   SP  - адрес спрайта
                ;   HL  - адрес экрана
                ;   DE  - адрес атрибутов
                ;   BC  - два батйта спрайта (значение маски и спрайта)
                JR .x6_

.x2             CHECK_UPDATE_COUMN                                              ; проверка обновления столбца
                RES 1, D
                RES 2, D

                LD (Column.ContainerSP), SP

                LD L, C
                LD H, B
                LD C, (HL)
                INC HL
                LD B, (HL)
                INC HL
                LD SP, HL

                ; DE  - адрес экрана
                LD L, D
                LD H, HIGH Adr.ScrAttrAdrTable
                LD H, (HL)
                LD L, E
                EX DE, HL

                ;   SP  - адрес спрайта
                ;   HL  - адрес экрана
                ;   DE  - адрес атрибутов
                ;   BC  - два батйта спрайта (значение маски и спрайта)
                JR .x2_
                ; -----------------------------------------
                ; нижний хвост
                ; -----------------------------------------
.x8             CHECK_UPDATE_COUMN                                              ; проверка обновления столбца
                LD (Column.ContainerSP), SP

                LD L, C
                LD H, B
                LD C, (HL)
                INC HL
                LD B, (HL)
                INC HL
                LD SP, HL

                ; DE  - адрес экрана
                LD L, D
                LD H, HIGH Adr.ScrAttrAdrTable
                LD H, (HL)
                LD L, E
                EX DE, HL

                ;   SP  - адрес спрайта
                ;   HL  - адрес экрана
                ;   DE  - адрес атрибутов
                ;   BC  - два батйта спрайта (значение маски и спрайта)

                _2_PIXELS_LINES                                                 ; отображение двух пиксельных строк знакоместа
.x6_            _2_PIXELS_LINES                                                 ; отображение двух пиксельных строк знакоместа
                _2_PIXELS_LINES                                                 ; отображение двух пиксельных строк знакоместа
.x2_            _2_PIXELS_LINES_                                                ; отображение двух пиксельных строк знакоместа (завершающий)
                ATTR_MASK                                                       ; отображение атрибутов знакоместа с маской
                STORE_HEIGHT_BOUNDARY                                           ; сбор высоты знакоместа

.BoundaryLoop   CHECK_TOP_SCR                                                   ; проверка достижения верхней границы (в знакоместах)
                ; -----------------------------------------
                ; вывод без маски
                ; -----------------------------------------
                PIXEL_UP_HL                                                     ; переход на знакоместо выше адрес адрес пикселей (в знакоместах)
                ATTR_UP_DE                                                      ; переход на строку выше адрес атрибутов

                _2_PIXELS_LINES                                                 ; отображение двух пиксельных строк знакоместа
                _2_PIXELS_LINES                                                 ; отображение двух пиксельных строк знакоместа
                _2_PIXELS_LINES                                                 ; отображение двух пиксельных строк знакоместа
                _2_PIXELS_LINES_                                                ; отображение двух пиксельных строк знакоместа (завершающий)
                ATTR_MASK                                                       ; отображение атрибутов знакоместа с маской
                STORE_HEIGHT_BOUNDARY                                           ; сбор высоты знакоместа
                SRA A                                                           ; флаг маски отвечает, отвечающий за последующее рисование
                                                                                ;   если флаг сброшен, следующиее идёт вывод без маски
                                                                                ;   если флаг установлен, следующее идёт вывод с маской
                JR NC, .BoundaryLoop                                            ; переход, если флаг рисования без маски

.Loop           EX AF, AF'
                CHECK_TOP_SCR                                                   ; проверка достижения верхней границы (в знакоместах)
                ; -----------------------------------------
                ; вывод с маской
                ; -----------------------------------------
                PIXEL_UP_HL                                                     ; переход на знакоместо выше адрес адрес пикселей (в знакоместах)
                ATTR_UP_DE                                                      ; переход на строку выше адрес атрибутов

                LD A, E
                EX AF, AF'
                CP #08
                JR NC, .Full                                                    ; переход, если рисуется всё знакоместо

                ; расчёт смещения для перехода
                LD E, A
                XOR A
                SUB E
                AND #07
                LD E, A

                ADD A, A    ; x2
                ADD A, E    ; x3
                ADD A, A    ; x6
                LD (.Jump), A

                EX AF, AF'
                LD E, A
.Jump           EQU $+1
                JR $

.Full           PIXEL_MASK
                PIXEL_MASK
                PIXEL_MASK
                PIXEL_MASK
                PIXEL_MASK
                PIXEL_MASK
                PIXEL_MASK
                PIXEL_MASK_
                
                ATTR_MASK                                                       ; отображение атрибутов знакоместа с маской

                ; проверка окончания данных спрайта
                OR A
                JR NZ, .Loop                                                    ; переход, если незакончились данные спрайта

                ; ; сохранение адреса спрайта
                ; LD HL, #0000
                ; ADD HL, SP

.ContainerSP    EQU $+1
.Exit           LD SP, #0000

.NextColumn     ; стартовый адрес таблицы смещений по горизонтали
.OffsetTable    EQU $+1
                LD HL, #0000
                
                EXX
                INC E
                PUSH DE                                                         ; сохранение, адреса строки экрана
                EXX

                ; чтение адреса спрайта
                LD C, (HL)
                INC HL
                LD B, (HL)
                INC HL
                LD (.OffsetTable), HL

                POP DE                                                          ; восстановить адрес экрана
                RET

                display " - Draw hexagon column:\t\t\t\t", /A, Column, "\t= busy [ ", /D, $-Column, " byte(s)  ]"

                endif ; ~ _DRAW_HEXAGON_COLUMN_