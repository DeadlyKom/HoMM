                ifndef _DRAW_HEXAGON_DISPLAY_LIST_GENERATION_
                define _DRAW_HEXAGON_DISPLAY_LIST_GENERATION_
; -----------------------------------------
; генерация списка отображения гексагонов
; In:
; Out:
; Corrupt:
; Note:
; -----------------------------------------
HexDLGeneration:; -----------------------------------------
                ; представление дисплейного списка
                ; +0 - первая рисуемая колонка первого гексагона (0-5)
                ; +1 - индекс/смещение в рендер буфере обрабатываемого гексагона
                ; +2 - индекс/смещение в рендер буфере обрабатываемого гексагона +80
                ; +3 - номер строки по вертикали в пикселях
                ; +4 - количество пропускаемых знакомест (спрайт находится ниже экрана)
                ; -----------------------------------------

                LD DE, DisplayList

                ; формирование цикла по вертикали:
                ;   7 строк, если 0÷3 или 4÷7
                ;   8 строк, если 3 или 8
                LD A, (.Shift_Y)
                AND #03
                CP #03
                SBC A, A
                ADD A, #08
                LD B, A
                LD (GameState.DisplayListLen), A                                ; сохранение длины списока отображения

                ; FWorldInfo.MapPosition.Y & 0x01
                LD A, (GameSession.WorldInfo + FWorldInfo.MapPosition.Y)
                AND #01
                DEC A
                LD (.VShiftTable), A

                ; .Shift_X * 2
.Shift_X        EQU $+1                                                         ; смещение по горизонтали в знакоместах (0-5)
                LD A, #00   ; .Shift_X
                ADD A, A    ; x2
                LD (.HShiftTable), A

                LD A, #04   ; нулевой гексагон рисуется на 3 знакоместа ниже + 1
.Shift_Y        EQU $+1     ; смещение по вертикали в знакоместах (0-7)
                SUB #00
                LD (.ShiftVertical), A

.RowsLoop       ; -----------------------------------------
                ; расчёт адреса таблицы в зависимости от смещения карты по горизонтали и вертикали
                ; ((номер строки + (FWorldInfo.MapPosition.Y & 0x01) - 1) & 0x01) * 12
                LD A, B
.VShiftTable    EQU $+1
                ADD A, #00  ; дополнительное смещение (0-1) - 1
                AND #01     ; 2 значения в таблице
                ADD A, A    ; x2
                LD C, A     
                ADD A, A    ; x4
                ADD A, C    ; x6
                LD C, A
                ADD A, A    ; x12
.HShiftTable    EQU $+1
                ADD A, #00
                ; смещение в таблице                                  
                ADD A, LOW .Table
                LD L, A
                ADC A, HIGH .Table
                SUB L
                LD H, A
                LD A, (HL)  ; количество рисуемых колонок (от 0 до 5)
                INC L
                LD (DE), A                                                      ; первая рисуемая колонка первого гексагона (0-5)
                INC E

                ; расчёт смещения обрабатываемой строки в рендер буфере
                LD A, B     ; номер строки
                DEC A       ; номер строки - 1
                LD C, A     ; сохранение номера строки - 1
                ADD A, A    ; x2
                ADD A, A    ; x4
                ADD A, C    ; x5
                LD C, A
                ADD A, (HL) ; 0 или 1
                INC L
                LD (DE), A                                                      ; индекс/смещение в рендер буфере обрабатываемого гексагона
                INC E

                LD A, C     ; x5
                ADD A, A    ; x10
                ADD A, B
                DEC A       ; x11
                ADD A, A    ; x22
                ADD A, #50  ; + 80
                LD (DE), A                                                      ; индекс/смещение в рендер буфере обрабатываемого гексагона +80
                INC E

                ; расчёт строки вывода гексагона
                LD A, B     ; номер строки
                DEC A       ; номер строки - 1
                ADD A, A    ; x2
                ADD A, A    ; x4
.ShiftVertical  EQU $+1
                ADD A, #00  ; нулевой гексагон рисуется на (3 знакоместа ниже + 1) - (смещение по вертикали в знакоместах (0-7))
                ADD A, A    ; x2
                ADD A, A    ; x4
                ADD A, A    ; x8
                OR #07      ; могло быть 1, 5 или 7    

                ; определение отображение колонки ниже видимой области
                LD H, #00
                CP (SCR_WORLD_SIZE_Y + SCR_WORLD_POS_Y) << 3
                JR C, .InView                                                   ; переход, если начало отображение колонки в видимой области
                
                ; расчёт количество пропускаемых знакомест
                LD L, (SCR_WORLD_SIZE_Y + SCR_WORLD_POS_Y) << 3
                SUB L
                DEC L                                                           ; установка строки равной самой нижней видемой строки
                
                ; округление до знакоместа
                SRL A
                ADC A, H
                RRA
                ADC A, H
                RRA
                ADC A, H
                LD H, A
                LD A, L

.InView         ; сохранение строки по вертикали в пикселях
                LD (DE), A
                INC E

                ; установка пропускаемых знакомест
                LD A, H
                LD (DE), A
                INC E

                DJNZ .RowsLoop

                LD (GameState.DisplayList), DE
                RET

.Table          ; первый байт, количество пропускаемых колонок первого гексагона (1-6), последующие вычисляются быстро
                ; второй байт, дополнительное смещение по горизонтали индекса в рендер буфере (0-1)
                ;
                ; |<-0->|  |<-1->|  |<-2->|  |<-3->|  |<-4->|  |<-5->|
                DB 3, 0,    4, 0,    5, 0,    0, 1,    1, 1,    2, 1            ; 0 смещение по вертикали в знакоместах
                DB 0, 0,    1, 0,    2, 0,    3, 0,    4, 0,    5, 0            ; 1 смещение по вертикали в знакоместах

                display " - Hexagon display list generation:\t\t\t", /A, HexDLGeneration, "\t= busy [ ", /D, $-HexDLGeneration, " byte(s)  ]"

                endif ; ~ _DRAW_HEXAGON_DISPLAY_LIST_GENERATION_

