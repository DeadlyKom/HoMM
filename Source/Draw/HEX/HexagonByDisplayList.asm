                ifndef _DRAW_HEXAGON_DISPLAY_LIST_
                define _DRAW_HEXAGON_DISPLAY_LIST_
; -----------------------------------------
; отображение гексагонов используя список отображения
; In:
; Out:
; Corrupt:
; Note:
; -----------------------------------------
HexByDL:        ; инициализация
                LD (HexByDL.Exit.ContainerSP), SP
                LD SP, CallSequence + 30
                LD IX, (GameState.DisplayList)
                LD IYH, HIGH Adr.RenderBuffer                                   ; для сохранения бит высоты столбца
                LD BC, HexByDL.Exit
                PUSH BC

                ; формирование цикла по вертикали (строк гексагонов)
                LD BC, .RowsLoop
                LD A, (GameState.DisplayListLen)
                NEG
                ADD A, #08
                LD (.RowsJump), A
.RowsJump       EQU $+1
                JR $
                PUSH BC
                PUSH BC
                PUSH BC
                PUSH BC
                PUSH BC
                PUSH BC
                PUSH BC

.RowsLoop       ; цикл отображения строк гексагонов

                ; переход к следующему элементу списка отображения
                LD BC, -DisplayList.ElementSize
                ADD IX, BC

                ; -----------------------------------------
                ; представление дисплейного списка
                ; +0 - первая рисуемая колонка первого гексагона (0-5)
                ; +1 - индекс/смещение в рендер буфере обрабатываемого гексагона
                ; +2 - индекс/смещение в рендер буфере обрабатываемого гексагона +80
                ; +3 - номер строки по вертикали в пикселях
                ; +4 - количество пропускаемых знакомест (спрайт находится ниже экрана)
                ; -----------------------------------------

                ; -----------------------------------------
                ; рассчёт адреса экранной области
                LD H, HIGH Adr.ScrAdrTable
                LD L, (IX + 3)                                                  ; номер строки по вертикали в пикселях
                LD E, (HL)
                INC E                                                           ; начало с 1 знакоместа
                INC H
                LD D, (HL)
                RES 7, D                                                        ; сброс бита, переход на основной экран

                ; -----------------------------------------
                ; чтение индекса/смещения в рендер буфере
                LD H, HIGH Adr.RenderBuffer
                LD L, (IX + 1)                                                  ; индекс/смещение в рендер буфере обрабатываемого гексагона

                LD A, (IX + 2)
                LD IYL, A
                ; -----------------------------------------
                LD B, SCR_WORLD_SIZE_X                                          ; ширина строки (константна)
                LD C, (IX + 0)                                                  ; номер первой рисуемой колонки первого гексагона (0-6)
.HexagonLoop    ; цикл отображения гексагонов

                ; C - счётчик отрисованных колонок гексангона,
                ;     но пока хранит номер колонки с которой рисуется гексагон
                ;
                ; если С = 0, гексагон рисуется полностью
                ; если С > 0, отсечение гексагона слева (гексагон рисуется с колонки под номером C)
                ; если С < 0, отсечение гексагона справа, с последующим обнулением (гексагон рисуется с 0 колонки)
                ;
                ; ⚠️ ВАЖНО ⚠️
                ;   влияет как на количество рисуемых колонок, так и с какой колонки будет рисоваться гексагон

                LD A, (HL)                                                      ; чтение значения флагов из рендер буфера (Adr.RenderBuffer)
                RES 7, (HL)                                                     ; сброс флага обновления гексагона
                EXX
                ; -----------------------------------------
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | HU | FG | .. | .. | A3 | A2 | A1 | A0 |
                ;   +----+----+----+----+----+----+----+----+
                ;   
                ;   HU      [7]         - флаг, принудительного обновления гексагона (частично или полностью)
                ;   FG      [6]         - флаг, тумана (0 - гексагон закрашен туманом целиком)
                ;   ⚠️ два флага HU и FG позволяют управлять процесом смены видимого гексагона на невидимый ⚠️
                ;   A3-A0   [3-0]       - номер анимации гексагона (локальный)
                ; -----------------------------------------

                ; проверка принудительного обновления гексагона (частично или полностью)
                ADD A, A                                                        ; выталкивание флага, принудительного обновления гексагона (частично или полностью)
                JR NC, .ToNextHexagon                                           ; переход, если нет необходимости обновлять гексагон (частично или полностью)

.Indexes        EQU $+1                                                         ; первый индекс цепочки индексов в буфере спрайтов (Adr.SpriteInfoBuffer) x2,
                LD L, #00                                                       ; последовательность индексов
                
                ; определение отображения тумана или гексагоного тайла
                ADD A, A                                                        ; выталкивание флага, видимости гексагона (целиком)
                JR C, .ReadIdxHextile                                           ; переход, если необходимо отобразить видемый гексагоный тайл

.FOG            ; -----------------------------------------
                ; отображение тумана (скрытый гексагонный тайл)
                EX AF, AF'                                                      ; сохранение номер анимации гексагона х4
                ; переход к базовым спрайтам гексагонов
                LD A, L
                SUB 2 << 1
                LD L, A
                ; установка спрайта тумана
                XOR A
                JR .IncBuffer

.ReadIdxHextile ; чтение индекса тайла гексагона
                EX AF, AF'                                                      ; сохранение номер анимации гексагона х4

                ; гексагон виден, определение спрайта
                EXX
                INC H                                                           ; переход к буферу тайловой карты (Adr.TilemapBuffer)
                LD A, (HL)                                                      ; чтение индекса тайла
                DEC H                                                           ; переход обратно к буферу рендера (Adr.RenderBuffer)
                EXX
.IncBuffer      ; -----------------------------------------
                ; расчёт адреса расположения спрайта
                ; структура хранения данных о расположении гексагонально тайла:
                ;   - первый байт, хранит страницу нахождения тайла 0-31, старшие 3 бита хранят индекс в данном списке
                ;   - второй байт, старший байт адреса памяти, т.к. младший всегда нулевой
                ; -----------------------------------------
                LD H, HIGH Adr.SpriteInfoBuffer >> 2
                ADD HL, HL  ; x4
                ADD A, L                                                        ; L - хранит индекс первого индекса (Adr.SpriteInfoBuffer) спрайтов тайлов x2
                LD L, A
                ADC A, H
                SUB L
                LD H, A
                ADD HL, HL  ; x8

                ; -----------------------------------------
                LD E, (HL)                                                      ; чтение первого байта, хранит страницу нахождения тайла 0-31, старшие 3 бита хранят индекс в данном списке
                ; -----------------------------------------
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | O2 | O1 | O0 | P4 | P3 | P2 | P1 | P0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   O2-O0   [7-3]       - смещение в блоковой таблице
                ;   P4-P0   [3-0]       - страница памяти
                ; -----------------------------------------

                ; включить страницу памяти расположения спрайта гексагона
                LD A, E
                AND %00011111                                                   ; отосечение страниц памяти
                SET_PAGE_A                                                      ; установка страницы спрайта

                INC L
                LD H, (HL)                                                      ; чтение второго байта, старший байт адреса памяти, т.к. младший всегда нулевой

                ; расчитать смещение в блоковая таблица смещений (0-7)
                LD A, E         ; oooppppp
                RRA             ; xooopppp
                RRA             ; xxoooppp
                RRA             ; xxxooopp
                AND %00011100   ; 000ooo00
                LD L, A

                ; чтение начального адреса расположения таблицы смещений гексагона (6 * 2 * 8 = 96 тайт)
                LD E, (HL)
                INC L
                LD D, (HL)

                ; расчёт адреса таблицы смещений гексагона с учётом кадра анимации
                ; 6 столбцов гексагона по 2 байта на адрес расположения,
                ; 12 байт на каждую анимацию. ранее было умножение на 4
                EX AF, AF'  ; xxАAAA00 (номер анимации)
                AND RENDER_FLAG_HEX_ANIM_MASK << 2
                LD C, A
                ADD A, A    ; x2
                ADD A, C    ; x3
                ADD A, E
                LD L, A
                ADC A, D
                SUB L
                LD H, A

                ; корректировка начального адреса спрайта 
                ; адрес спрайта = начальный адрес + номер анимация * 12 байт + горизонтальное смещение (0-5 номер столбца) * 2
                EXX

                ; C - счётчик отрисованных колонок гексангона,
                ;     но пока хранит номер колонки с которой рисуется гексагон
                ;
                ; если С = 0, гексагон рисуется полностью
                ; если С > 0, отсечение гексагона слева (гексагон рисуется с колонки под номером C)
                ; если С < 0, отсечение гексагона справа, с последующим обнулением (гексагон рисуется с 0 колонки)
                ;
                ; ⚠️ ВАЖНО ⚠️
                ;   влияет как на количество рисуемых колонок, так и с какой колонки будет рисоваться гексагон

                LD A, C                                                         ; номер колонки с какой рисуется гексагон
                EX AF, AF'                                                      ; сохранение количества колонок гексагона которое необходимо пропустить
                LD A, C                                                         ; номер колонки с какой рисуется гексагон
                LD C, #00                                                       ; сброс счётчика отрисованных колонок гексангона
                ADD A, A
                JR NC, $+7                                                      ; переход, если гексагон отсекается слева или рисется полностью

                ; гексагон отсекается справа
                RRA
                NEG
                EX AF, AF'                                                      ; сохранение количества колонок гексагона которое необходимо пропустить
                XOR A                                                           ; отображение гексагона с 0ой колонки

                PUSH DE                                                         ; сохранение, адреса строки экрана
                EXX
                POP DE                                                          ; восстановить адрес экрана

                ; применение смещение (горизонтальное 0-5 столбца) внутри таблицы смещений гексагона
                ADD A, L
                LD L, A
                ADC A, H
                SUB L
                LD H, A

                ; чтение адреса спрайта
                LD C, (HL)
                INC HL
                LD B, (HL)
                INC HL
                LD (Column.OffsetTable), HL

.DrawHexagon    ; отображение гексагона
                LD HL, .NextHexagon
                PUSH HL

                ; формирование цикла по горизонтали (колонки гексагона)
                LD HL, EntryPointDraw
                EX AF, AF'                                                      ; восстановление номера колонки с которой рисуется гексагон
                LD (.DrawColumnJump), A
.DrawColumnJump EQU $+1
                JR $
                PUSH HL
                PUSH HL
                PUSH HL
                PUSH HL
                PUSH HL
                ;   SP - адрес следующей функции
                ;   DE - адрес экрана
                ;   BC - адрес спрайта
                ;   IX - адрес списка отображения
                ;   IY - адрес Adr.RenderBuffer + 80 (для сохранения бит высоты столбца)
                JP (HL)

.ToNextHexagon  ; переход к следующему гексагону
                EXX
                ; C - счётчик отрисованных колонок гексангона,
                ;     но пока хранит номер колонки с которой рисуется гексагон
                ;
                ; если С = 0, гексагон рисуется полностью
                ; если С > 0, отсечение гексагона слева (гексагон рисуется с колонки под номером C)
                ; если С < 0, отсечение гексагона справа, с последующим обнулением (гексагон рисуется с 0 колонки)
                ;
                ; ⚠️ ВАЖНО ⚠️
                ;   влияет как на количество рисуемых колонок, так и с какой колонки будет рисоваться гексагон
                
                ; расчитываем оставшиеся количество колонок в гексагоне
                LD A, C
                OR A
                JR Z, .FullHex
                JP M, $+5
                NEG                                                             ; при смене знака, число указывает количество пропускаемых колонок гексагона
.FullHex        ADD A, HEXTILE_SIZE_X
                LD C, A
                
                ; следующий гексагон
                ADD A, IYL
                LD IYL, A

                ; следующий адрес гексагона
                LD A, C
                ADD A, E
                LD E, A

                EXX

.NextHexagon    ; следующий гексагон

                ; HL' - адрес обрабатываемого гексагона в рендер буфере
                ; DE' - адрес экрана
                ; BC' - счётчики (B - оставшийся ширины, C - количество нарисованных столбцов гексагона)

                EXX
                INC L                                                           ; переход к следующему гексогону

                LD A, B
                SUB C                                                           ; счётчика отрисованных колонок гексангона
                RET Z                                                           ; выход, если был отрисована последний гексагон в строке
                
                LD C, #00
                LD B, A
                CP HEXTILE_SIZE_X
                JP NC, .HexagonLoop                                             ; переход, если можно отобразить гексагон целиком

                ADD A, -HEXTILE_SIZE_X
                LD C, A
                JP .HexagonLoop
HexByDL.Exit:   ;
.ContainerSP    EQU $+1
                LD SP, #0000
                RET

                display " - Draw hexagon by display list:\t\t\t", /A, HexByDL, "\t= busy [ ", /D, $-HexByDL, " byte(s)  ]"

                endif ; ~ _DRAW_HEXAGON_DISPLAY_LIST_

