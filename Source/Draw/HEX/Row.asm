                ifndef _DRAW_HEXAGON_ROW_
                define _DRAW_HEXAGON_ROW_
; -----------------------------------------
; отображение гексагонального строки
; In:
;   IY - адрес рендер буфера    (Adr.RenderBuffer)
;   DE - адрес строки экрана
;   С  - смещение по горизонтали (от начала 0-5)
;   A  - смещение по горизонтали (от -5 до 5)
;          если = 0, гексагон рисуется полностью
;          если > 0, отсечение гексагона слева
;          если < 0, отсечение гексагона справа
;   A' - количество пропускаемых знакомест
; Out:
; Corrupt:
; Note:
;   отображение производится снизу вверх
;
;   структура хранения спрайта с анимациями:
;    - каждый FSprites указывает максимум на 4 гексагонов, без учёта анимации
;    - для каждого гексагона хранится 12 байт, по 2 байта на 6 столбцов
;    - если гексагон имеет анимацию, то адреса начала спрайтов смещаются на 12 * количество анимаций
;      пример
;      a) отсутствуют анимации, первые 12 байт отвечают за адреса начала каждого из столбцов гексагона
;      б) две анимации, 12 * 2 байт отвечают на адреса начала каждого из столбцов гексагона
;         где первые 12 байт для 0-ой анимации, следующие 12 байт для 1-ой анимации
;         максимум 8 анимаций в последовательности (96 байт)
;
;   структура спрайта в сыром виде:
;    - 2 байта смещение от начала до каждого из столбцов
;      12 байт смещений, далее идут сырые данные спрайта
;    - сырые данные хранятся снизу вверх, для удобства рисования их на экране
;    - первые 2/6/8 байт рисуются всегда без маски, далее идут 2 байта атрибут
;      первый байт хранит данные о цвете, где старший 7-ой бит отвечает о необходимости принудительно обновить атрибут
;      второй байт метаданные знакоместа
;
;          7    6    5    4    3    2    1    0
;       +----+----+----+----+----+----+----+----+
;       | D5 | D4 | D3 | D2 | D1 | D0 | DM | HG |
;       +----+----+----+----+----+----+----+----+
;
;       HG      [0]         - флаг, высота знакоместа, учитывается при наложении тумана
;                               0 - низкое знакоместо, обвалакивается туманом
;                               1 - высокое знакоместо, туман не рисуется
;       DM      [1]         - флаг,  отвечает, за вывод последующее знакоместа
;                               0 - вывод без маски
;                               1 - вывод с маской
;       D5-D0   [2-0]       - высоту последующего вывода с маской
;                             нулевая высота, говорит о завершении отрисовкаи спрайта с маской,
;                             либо высота меньше 8 и ранний выход
;      ⚠️ ВАЖНО ⚠️
;      если идут данные вывода с маской 0 и 1 биты отсутствуют, байт хранит высоту вывода с маской
;      нулевая высота, либо высота меньше 8, говорит о завершении отрисовкаи спрайта с маской
;
;   структура хранения данных Adr.RenderBuffer:
;    * для хранения информации о гексагональном тайле требуется 7 байт, где
;      - байт в Adr.RenderBuffer'е по индексу хранит информацио о выводе
;
;          7    6    5    4    3    2    1    0
;       +----+----+----+----+----+----+----+----+
;       | HU | FG | .. | .. | .. | A2 | A1 | A0 |
;       +----+----+----+----+----+----+----+----+
;   
;       HU      [7]         - флаг, принудительного обновления гексагона (частично или полностью)
;       FG      [6]         - флаг, тумана (0 - гексагон закрашен туманом целиком)
;       ⚠️ два флага HU и FG позволяют управлять процесом смены видимого гексагона на невидимый ⚠️
;       A2-A0   [2-0]       - номер анимации гексагона (локальный)
;      - 1-6 байт по байту на каждый столбцов гексагона, хранят биты высоты столбца (бит на знакоместо высоты)
;        адрес = Adr.RenderBuffer + 64 + номер строки * 22 + номер знакоместа (0-4) * 6 + номер столбца (0-5)
;
;       ℹ️ структура байта в начале рисования столбца
;          7    6    5    4    3    2    1    0
;       +----+----+----+----+----+----+----+----+
;       | 0  | 0  | 0  | 0  | 0  | 0  | 0  | CU |
;       +----+----+----+----+----+----+----+----+
;
;       CU      [0]         - флаг, необходимости обновить столбец гексагона (0 - обновление столбца не требуется)
;
;       ℹ️ структура байта в конце, если столбец обновлён
;          7    6    5    4    3    2    1    0
;       +----+----+----+----+----+----+----+----+
;       | B7 | B6 | B5 | B4 | B3 | B2 | B1 | B0 |
;       +----+----+----+----+----+----+----+----+
;
;       B7-B0   [7-0]       - последовательность битов снизу вверх,
;                             информация высоты гексагона
;
; -----------------------------------------
Row:            ; A - смещение по горизонтали (от -5 до 5)
                ;   если = 0, гексагон рисуется полностью
                ;   если > 0, отсечение гексагона слева
                ;   если < 0, отсечение гексагона справа

                ; проверка левого отсечения
                OR A
                JR Z, .RowLoop                                                  ; переход, если левое отсечение отсутствует

                ; -----------------------------------------
                ; отобрражение строки с левым отсечением
                ; -----------------------------------------

                ; формирование цикла по горизонтали с урезанной левой частью
                LD A, #06
                SUB C

                EXX
                LD HL, .Sequent
                PUSH HL
                LD HL, Column.x8
                LD BC, Column.x6
                LD DE, Column.x2
                PUSH DE ; x2
                DEC A
                JR Z, .Begin
                PUSH BC ; x6
                DEC A
                JR Z, .Begin
                PUSH HL ; x8
                DEC A
                JR Z, .Begin
                PUSH HL ; x8
                DEC A
                JR Z, .Begin
                PUSH BC ; x6
                JR .Begin

.RowLoop        ; -----------------------------------------
                ; цикл отобрражение строки
                ; -----------------------------------------
                
                EXX
                LD HL, .Sequent
                PUSH HL
                EXX

                ; формирование цикла по горизонтали
                LD A, C
                ; A - смещение по горизонтали (от -5 до 5)
                ;   если = 0, гексагон рисуется полностью
                ;   если > 0, отсечение гексагона слева
                ;   если < 0, отсечение гексагона справа
.RightCut       EXX
                LD HL, Column.x8
                LD BC, Column.x6
                LD DE, Column.x2
                LD (.Jump), A
.Jump           EQU $+1
                JR $
                PUSH DE ; x2
                PUSH BC ; x6
                PUSH HL ; x8
                PUSH HL ; x8
                PUSH BC ; x6
                PUSH DE ; x2
.Begin          ; -----------------------------------------
                ; начало отображение гексагона
                ; -----------------------------------------
                EXX
                ;   
                LD A, (IY + 0)                                                  ; чтение значения флагов из буферу рендера (Adr.RenderBuffer)
                ; LD A, %11000000 ; временно
                ; -----------------------------------------
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | HU | FG | .. | .. | .. | A2 | A1 | A0 |
                ;   +----+----+----+----+----+----+----+----+
                ;   
                ;   HU      [7]         - флаг, принудительного обновления гексагона (частично или полностью)
                ;   FG      [6]         - флаг, тумана (0 - гексагон закрашен туманом целиком)
                ;   ⚠️ два флага HU и FG позволяют управлять процесом смены видимого гексагона на невидимый ⚠️
                ;   A2-A0   [2-0]       - номер анимации гексагона (локальный)
                ; -----------------------------------------

                ; проверка принудительного обновления гексагона (частично или полностью)
                ADD A, A                                                        ; выталкивание флага, принудительного обновления гексагона (частично или полностью)
                JR NC, .NextHexagon                                             ; переход, если нет необходимости обновлять гексагон (частично или полностью)
                ; RES 7, (IY + 0)                                                 ; сброс флага обновления гексагона

                ; проверка видимости гексагона (целиком)
                ADD A, A                                                        ; выталкивание флага, видимости гексагона (целиком)
                JR C, .VisibleHexagon                                           ; переход, если гексагон виден

.FOG            ; установка спрайта тумана
                EX AF, AF'
                XOR A
                JR .CalcSpriteInfo

.VisibleHexagon EX AF, AF'                                                      ; сохранение, для корректировки адреса локальной анимации
                ; гексагон виден, определение спрайта

                ; ToDo если сместить хранение тайлов на 128 байт, а значения рендер буфера оставить на месте,
                ;      то можно обойтись одним смещением, без INC/DEC IYH
                INC IYH                                                         ; переход к буферу тайловой карты (Adr.TilemapBuffer)
                LD A, (IY + 0)                                                  ; чтение индекса тайла
                DEC IYH                                                         ; переход обратно к буферу рендера (Adr.RenderBuffer)
                INC IYL                                                         ; переход к следующему гексогону

                ; -----------------------------------------
.CalcSpriteInfo ; расчёт адреса расположения спрайта
                ; структура хранения данных о расположении гексагонально тайла:
                ;   - первый байт, хранит страницу нахождения тайла 0-31, старшие 3 бита хранят индекс в данном списке
                ;   - второй байт, старший байт адреса памяти, т.к. младший всегда нулевой
                ; -----------------------------------------
                EXX
.Indexes        EQU $+1                                                         ; первый индекс цепочки индексов в буфере спрайтов (Adr.SpriteInfoBuffer),
                ADD A, #00                                                      ; последовательность индексов (заранее умноженное на 8)
                ADD A, A    ; x2
                LD L, A
                LD H, HIGH Adr.SpriteInfoBuffer

                ; -----------------------------------------
                LD E, (HL)                                                      ; чтение первого байта, хранит страницу нахождения тайла 0-31, старшие 3 бита хранят индекс в данном списке
                LD A, E

                ; -----------------------------------------
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | O2 | O1 | O0 | P4 | P3 | P2 | P1 | P0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   O2-O0   [7-3]       - смещение в блоковой таблице
                ;   P4-P0   [3-0]       - страница памяти
                ; -----------------------------------------
                AND %00011111
                SET_PAGE_A                                                      ; установка страницы спрайта
                INC L
                LD H, (HL)                                                      ; чтение второго байта, старший байт адреса памяти, т.к. младший всегда нулевой

                LD A, E         ; oooppppp
                RRA             ; xooopppp
                RRA             ; xxoooppp
                RRA             ; xxxooopp
                RRA             ; xxxxooop
                AND %00001110   ; 0000ooo0
                LD L, A

                ; чтение начального адреса расположения таблицы смещений гексагона (6 * 2 * 8 = 96 тайт)
                LD E, (HL)
                INC L
                LD D, (HL)

                ; расчёт адреса таблицы смещений гексагона с учётом кадра анимации
                ; 6 столбцов гексагона по 2 байта на адрес расположения,
                ; 12 байт на каждую анимацию. ранее было умножение на 4
                EX AF, AF'  ; xxxAAA00 (номер анимации)
                AND RENDER_FLAG_HEX_ANIM_MASK << 2
                LD C, A
                ADD A, A    ; x2
                ADD A, C    ; x3
                ADD A, E
                LD L, A
                ADC A, D
                SUB L
                LD H, A

                ; корректировка начального адреса спрайта 
                ; адрес спрайта = начальный адрес + номер анимация * 12 байт + горизонтальное смещение (0-5 номер столбца) * 2
                EXX
                LD A, C
                LD C, #00
                ADD A, A    ; x2
                PUSH DE                                                         ; сохранение, адреса строки экрана
                EXX

                ; применение смещение (горизонтальное 0-5 столбца) внутри таблицы смещений гексагона
                ADD A, L
                LD L, A
                ADC A, H
                SUB L
                LD H, A

                ; чтение адреса спрайта
                LD C, (HL)
                INC HL
                LD B, (HL)
                INC HL
                LD (Column.OffsetTable), HL

.DrawHexagon    ; -----------------------------------------
                ; отображение гексагона
                ; -----------------------------------------
                POP DE                                                          ; восстановить адрес экрана

                ; обновление количество пропускаемых знакомест
                EXX
                LD A, H
                OR A
                EX AF, AF'
                EXX

                RET

.NextHexagon    JR$

.Sequent        ; -----------------------------------------
                ; обработчик завершения строки
                ; -----------------------------------------

                ;   DE  - адрес экрана
                ;   BC  - адрес спрайта
                ;   HL' - адрес рендер буфера    (Adr.RenderBuffer)
                ;   DE' - адрес строки экрана
                ;   C'  - смещение по горизонтали      

                EXX
                LD A, B
                SUB C
                LD B, A
                LD C, #00
                JP P, .RowLoop                                                  ; переход, если можно отобразить гексагон целиком

                ; B - отрицательное <6
                ADD A, #06
                LD B, A
                LD A, #06
                SUB B
                JP .RightCut
Row.Size        EQU $-Row
                display " - Draw hexagon row:\t\t\t\t\t", /A, Row, "\t= busy [ ", /D, Row.Size, " byte(s)  ]"

                endif ; ~ _DRAW_HEXAGON_ROW_