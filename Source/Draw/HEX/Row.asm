                ifndef _DRAW_HEXAGON_ROW_
                define _DRAW_HEXAGON_ROW_
; -----------------------------------------
; отображение гексагонального строки
; In:
;   HL - адрес рендер буфера    (Adr.RenderBuffer)
;   DE - адрес строки экрана
;   BC - адрес таблицы смещений
; Out:
; Corrupt:
; Note:
;   отображение производится снизу вверх
;
;   структура хранения спрайта с анимациями:
;    - каждый FSpritesRef указывает максимум на 16 гексагонов, без учёта анимации
;    - для каждого гексагона хранится 12 байт, по 2 байта на 6 столбцов
;    - если гексагон имеет анимацию, то адреса начала спрайтов смещаются на 12 * количество анимаций
;      пример
;      a) отсутствуют анимации, первые 12 байт отвечают за адреса начала каждого из столбцов гексагона
;      б) две анимации, 12 * 2 байт отвечают на адреса начала каждого из столбцов гексагона
;         где первые 12 байт для 0-ой анимации, следующие 12 байт для 1-ой анимации
;         максимум 8 анимаций в последовательности (96 байт)
;
;   структура спрайта в сыром виде:
;    - 2 байта смещение от начала до каждого из столбцов
;      12 байт смещений, далее идут сырые данные спрайта
;    - сырые данные хранятся снизу вверх, для удобства рисования их на экране
;    - первые 2/6/8 байт рисуются всегда без маски, далее идут 2 байта атрибут
;      первый байт хранит данные о цвете, где старший бит отвечает о необходимости принудительно обновить атрибут
;      второй байт дополнительная информаци, где 
;       * 0-ой бит отвечает, за вывод последующее знакоместа,
;           если 0, вывод без маски
;           если 1, вывод с маской
;       * 1-ый бит отвечает за высоту знакоместа, учитывается при наложении тумана
;    - (8 + 2) + (8 + 2) байт идут согласно описанию выше
;    - последние (8 + 2) байта, в битах 2-7 (2-ой байт атрибута) хранят высоту последующего вывода с маской
;    - повторяется пока во 2-ом байте биты 2-7 не будут равны нулю
;
; -----------------------------------------
Row:            ; 
                LD A, (HL)
                ; -----------------------------------------
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | HU | TV | .. | .. | .. | A2 | A1 | A0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   HU      [7]         - флаг, обновления гексагона (целиком или частично)
                ;   TV      [6]         - флаг, видимости гексагона (целиком)
                ;   A2-A0   [2-0]       - номер анимации тайла (локальный)
                ; -----------------------------------------

                ; проверка необходимости обновления гексагона (целиком или частично)
                ADD A, A    ; << 1
                JR NC, .NextHexagon                                             ; переход, если нет необходимости обновлять гексагон (частично или полностью)
                RES 7, (HL)                                                     ; сброс флага обновления гексагона

                ; проверка видимости гексагона (целиком)
                ADD A, A    ; << 1
                JR NC, .VisibleHexagon                                          ; переход, если гексагон виден

.FOG            ; установка спрайта тумана
                EX AF, AF'
                XOR A
                JR .CalcSpriteInfo

.VisibleHexagon EX AF, AF'                                                      ; сохранение, для корректировки адреса локальной анимации
                ; гексагон виден, определение спрайта
                INC H                                                           ; переход к буферу тайловой карты (Adr.TilemapBuffer)
                LD A, (HL)                                                      ; чтение индекса тайла
                DEC H                                                           ; переход обратно к буферу рендера (Adr.RenderBuffer)

                ; -----------------------------------------
.CalcSpriteInfo ; расчёт адреса расположения FSpritesRef
                ; -----------------------------------------
                EXX
                ; -----------------------------------------
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | O3 | O2 | O1 | O0 | G3 | G2 | G1 | G0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   O3-O0   [7-4]       - смещение в группе индексов
                ;   G3-G0   [3-0]       - группа индексов спрайтов расположенная в буфере спрайтов (Adr.SpriteInfoBuffer)
                ; -----------------------------------------
                LD C, A
                AND #0F
.Indexes        EQU $+1                                                         ; первый индекс в цепочке индексы спрайтов гексагонов в буфере спрайтов (Adr.SpriteInfoBuffer)
                ADD A, #00                                                      ; последовательность 16 индексов
                ADD A, A    ; x2
                LD L, A
                LD H, HIGH Adr.SpriteInfoBuffer >> 2
                ADD HL, HL  ; x4
                ADD HL, HL  ; x8

                ; -----------------------------------------
                ; применение структуры FSpritesRef и корректировка конечного адреса спрайта
                ; -----------------------------------------
                INC L                                                           ; пропуск FSpritesRef.Num
                ; чтение страницы расположения данных о структурах
                LD A, (HL)                                                      ; FSpritesRef.Data.Page
                INC L
                SET_PAGE_A                                                      ; установка страницы спрайта
                ; корректировка адреса расположения необходимой структуры FSprite
                LD A, C
                AND #F0
                ADD A, (HL)                                                     ; FSpritesRef.Data.Adr (LOW)
                LD E, A
                INC L
                ADC A, (HL)                                                     ; FSpritesRef.Data.Adr (HIGH)
                SUB E
                LD D, A

                ; -----------------------------------------
                ; корректировка адреса от требуемой анимации
                ; -----------------------------------------
                ; 6 столбцов гексагона по 2 байта на адрес расположения,
                ; 12 байт на каждую анимацию. ранее было умножение на 4
                ; -----------------------------------------
                ; корректировка начального адреса спрайта 
                ; адрес спрайта = начальный адрес + номер анимация * 12 байт + горизонтальное смещение (0-5 номер столбца) * 2
                EXX
                LD A, (BC)                                                      ; чтение смещение по горизонтали * 2
                INC C

                PUSH DE
                EXX
                LD B, A                                                         ; аккумулятор хранит смещение в таблице CallSequence
                ; -----------------------------------------
                EX AF, AF'  ; xxxAAA00 (номер анимации)
                AND %00011100
                LD C, A
                ADD A, A    ; x2
                ADD A, C    ; x3
                ADD A, B    ; добавление смещения начального адреса спрайта
                ADD A, E
                LD L, A
                ADC A, D
                SUB L
                LD H, A

                ; -----------------------------------------
                ; чтение адреса спрайта
                LD C, (HL)
                INC HL
                LD B, (HL)

.DrawHexagon    ; отображение гексагона
                POP DE                                                          ; восстановить адрес экрана

                EX AF, AF'
                ADD A, LOW CallSequence.Copy
                LD L, A
                LD H, HIGH CallSequence.Copy
                LD SP, HL
                RET

.NextHexagon    ;   HL - адрес рендер буфера    (Adr.RenderBuffer)
                ;   DE - адрес строки экрана
                ;   BC - адрес таблицы смещений
                
                LD A, (BC)
                NEG
                ADD A, #06
                PUSH AF
                ADD A, E
                LD E, A
                DEC E
                POP AF
                ADD A, C
                LD C, A

.Sequent        INC L

                INC E
                LD A, E
                CP 22 + 1
                JP C, Row

.NextRow        RET

                display " - Draw hexagon row:\t\t\t\t\t", /A, Row, "\t= busy [ ", /D, $-Row, " byte(s)  ]"

                endif ; ~ _DRAW_HEXAGON_ROW_