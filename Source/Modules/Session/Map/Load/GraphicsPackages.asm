
                ifndef _MODULE_SESSION_MAP_LOAD_GRAPHICS_PACKAGES_
                define _MODULE_SESSION_MAP_LOAD_GRAPHICS_PACKAGES_
; -----------------------------------------
; загрузка графических пакетов
; In:
;   HL  - адрес блока данных необходимых графических пакетов для текущей карты
;   D   - старший адрес буфера равный для размещения загруженных пакетов
;         выровнен 256 байт
;   BC  - размер блока данных необходимых графических пакетов для текущей карты
;   HL' - адрес блока данных таблицы сопоставления гексагонального тайла и графического пакета
;   BC' - размер блока данных таблицы сопоставления гексагонального тайла и графического пакета
; Out:
;   A'  - количество загруженных спрайтов
; Corrupt:
; Note:
;   - включена страница загруженной карты!
;   - размер блока данных необходимых графических пакетов для текущей карты, не должен превышать 128
; -----------------------------------------
GraphicsPackages:
                PUSH_PAGE                                                       ; сохранение номера страницы в стеке
                
                ; копирование блока данных необходимых графических пакетов для текущей карты
                LD E, #00
                LD A, C                                                         ; копирование количество идентификаторов графических пакетов для текущей карты
                LDIR

                ; обнуление значений страших адресов
                DEC E
                SET 7, E
                LD C, A
                LD H, D
                LD L, E
                DEC E
                LD (HL), B
                DEC C
                JR Z, $+4
                LDDR

                RES 7, L
                LD B, A

                ; обнуление количества загруженных спрайтов
                XOR A
                EX AF, AF'

.Loop           PUSH BC
                
                ; загрузка ассета
                LD E, (HL)                                                      ; чтение ID ассета
                CALL Session.SharedCode.Load.GraphicAsset
                
                ; добавить количество загруженых спрайтов в общий список
                LD B, A
                EX AF, AF'
                ADD A, B
                EX AF, AF'

                SET_MODULE_PAGE_Session                                         ; включить страницу модуля "Session"

                ; инициализация буфера новыми значениями
                ; позволит избавится от лишних обращений к ассет менеджеру
                LD A, (GameState.Assets + FAssets.Address.Page)
                LD (HL), A
                SET 7, L
                LD A, (GameState.Assets + FAssets.Address.Adr.High)
                LD (HL), A
                RES 7, L
                INC L

                POP BC
                DJNZ .Loop

                JP_POP_PAGE                                                     ; восстановление номера страницы из стека
; -----------------------------------------
; загрузка графического пакета с корректировкой адресов
; In:
;   E - ID загружаемого ассета
; Out:
;   A - количество спрайтов в пакете
; Corrupt:
; Note:
;   - включена страница загруженной карты!
;   - размер блока данных необходимых графических пакетов для текущей карты, не должен превышать 128
; -----------------------------------------
GraphicAsset:   PUSH HL
                PUSH BC

                ; загрузка ассета
                SET_PAGE_ASSETS                                                 ; включить страницу расположения ассет менеджера
                LOAD_ASSETS_REG E                                               ; загрузка ресурса
                ; HL - адрес загрузки/распаковки

                ; -----------------------------------------
                ; инициализация таблицы смещений
                ; после загрузки ассета в память его смещения преобразуются в абсолютные адреса
                ; -----------------------------------------

                ; чтение количество гексагональных тайлов в блоке
                ; значение всегда находится по смещению 0x0020
                LD L, #20
                LD B, (HL)                                                      ; количество гексагональных тайлов в блоке
                LD L, #00
                PUSH BC

.BlockTableLoop ; -----------------------------------------
                ; цикл обхода блоковой таблицы смещений
                PUSH BC

                ; преобразование группы гексагональных тайлов относительные адреса в абсолютные
                LD E, (HL)
                INC L
                LD D, (HL)
                EX DE, HL
                ADD HL, DE
                DEC HL                                                          ; программный декремент
                EX DE, HL
                LD (HL), D
                DEC L
                LD (HL), E
                INC L
                INC L
                ; чтение данных о таблице смещений и флаги
                LD C, (HL)                                                      ; чтение количество анимаций в таблице смещений
                INC L
                ; LD B, (HL)                                                      ; чтение флагов ?
                INC L
                PUSH HL
                EX DE, HL
                ; HL - указывает на первый элемент таблицы смещений обрабатываемого спрайта
                PUSH HL
                POP IX

                LD DE, 192

                ; определение наличие анимации в первой и во второй половине
                ; сли анимации имеются хоть в одной половинке, таблица смещений должна быть полной
                ; и смещение до данных спрайта лежат ровно на 192 байта впереди иначе, смещение хранит нужное смещение
                LD A, C
                RRA
                RRA
                RRA
                RRA
                OR C
                AND #0F
                OR A
                JR NZ, $+4                                                      ; переход, если имеются анимации
                LD E, 12+2

                EX AF, AF'
                ADD HL, DE
                EX AF, AF'
                EX DE, HL
                JR Z, .OmeSprite

                ; HL - указывает на данные спрайта
                ; IX - указывает на таблицу смещений

                ; чтение длины блока данных байта
                EX DE, HL
                LD C, (HL)
                INC HL
                LD B, (HL)
                INC HL
                EX DE, HL

                ; -----------------------------------------
                ; цикл обхода таблицы смещений
                dup 15
                CALL ColumnOffset
                edup
.OmeSprite      CALL ColumnOffset
                ; -----------------------------------------

                POP HL

                POP BC
                DJNZ .BlockTableLoop
                POP AF

                POP BC
                POP HL

                RET

                ; HL - адрес блоку данных спрайта
                ; BC - длина блока данных спрайта
                ; IX - адрес смещения
ColumnOffset:   ; проверка первого пустого смещения
                LD HL, (IX + 2)                                                 ; чтение 2х байт смещения (не нулевой колонки)
                LD A, L
                OR H
                JR Z, .Next                                                     ; переход, если смещение отсутствует

.Offset         defl 0
                dup 6
                LD HL, (IX + .Offset)
                ADD HL, DE
                LD (IX + .Offset), HL
.Offset         = .Offset + 2
                edup

                ; переход к следующему блоку данных спрайта
                EX DE, HL
                ADD HL, BC

                ; чтение длины блока данных байта
                LD C, (HL)
                INC HL
                LD B, (HL)
                INC HL
                EX DE, HL

.Next           ; смещение адреса в таблице смещений к следующему значению
                EX DE, HL
                LD DE, 12
                ADD IX, DE
                EX DE, HL
                RET                                                             ; ранний выход, т.к. пустое смещение

                display " - Load graphics packages:\t\t\t\t", /A, GraphicsPackages, "\t= busy [ ", /D, $-GraphicsPackages, " byte(s)  ]"

                endif ; ~_MODULE_SESSION_MAP_LOAD_GRAPHICS_PACKAGES_