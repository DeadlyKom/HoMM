
                ifndef _MODULE_SESSION_INITIALIZE_GRAPHICS_PACKAGES_
                define _MODULE_SESSION_INITIALIZE_GRAPHICS_PACKAGES_
; -----------------------------------------
; инициализация графических пакетов
; In:
;   Adr.TilemapBuffer - хранит информацию о загруженных графических ассетах
;   D   - старший адрес буфера равный для размещения загруженных пакетов
;         выровнен 256 батйт
;   HL' - адрес блока данных таблицы сопоставления гексагонального тайла и графического пакета
;   BC' - размер блока данных таблицы сопоставления гексагонального тайла и графического пакета
;   A   - стартовый индекс гексагональных тайлов в буфере спрайтов (Adr.SpriteInfoBuffer) x2
; Out:
; Corrupt:
; Note:
;   - включена страница загруженной карты!
;   - размер блока данных необходимых графических пакетов для текущей карты, не должен превышать 128
; -----------------------------------------
Initialize.GraphicsPackages:
                ; расчёт начального адреса сохранения значений                                  
                LD L, A
                LD H, HIGH Adr.SpriteInfoBuffer >> 2
                ADD HL, HL  ; x4
                ADD HL, HL  ; x8

                EXX
                ; приведение размер блока данных таблицы сопоставления гексагонального тайла и графического пакета к байту,
                ; где значения 0-255 соответствуют размеру, а 0 означает 256 тайлов
                RR B
                RR C
                LD B, C

.Loop           ; таблица сопоставления гексагональных тайлов и графического пакетов, где
                ;  1. индекс массива - ID гексагонального тайла
                ;  2. первый байт - индекс в массиве FMapHeader.GraphicPack
                ;  3. второй байт - смещение внутри текущего графического пакета (0-7)
                ;
                ;   | <---------- первый байт ---------- > |   | <---------- второй байт ---------- >  |
                ;
                ;  +----+----+----+----+----+----+----+----+   +----+----+----+----+----+----+----+----+
                ;  | 15 | 14 | 13 | 12 | 11 | 10 |  9 |  8 |   |  7 |  6 |  5 |  4 |  3 |  2 |  1 |  0 |
                ;  +----+----+----+----+----+----+----+----+   +----+----+----+----+----+----+----+----+
                ;  | UB | I6 | I5 | I4 | I3 | I2 | I1 | I0 |   | O2 | O1 | O0 | .. | .. | .. | .. | .. |
                ;  +----+----+----+----+----+----+----+----+   +----+----+----+----+----+----+----+----+
                ;
                ;   UB      [15]        - флаг, использования гесагонального тайла (1 - не используется)
                ;   I6-I0   [14..8]     - ндекс в массиве FMapHeader.GraphicPack
                ;   O2-O0   [7..5]      - смещение внутри текущего графического пакета (0-7)
                ;   
                LD E, (HL)                                                      ; чтение первого байта
                INC HL
                LD D, (HL)                                                      ; чтение второго байта
                INC HL
                PUSH DE
                EXX
                POP BC

                ; проверка использования тайла
                BIT MAP_HEXTILE_USABLE_BIT, C
                JR NZ, .NextHextile                                             ; переход, если гексагональный тайл не используется

                ; -----------------------------------------
                ; чтение данных о физическом расположении ассета в памяти
                ; индекс ассета, соответствует индексу в таблице Adr.TilemapBuffer,
                ; где младшие (0-127) хранят страницу, а старшие (128-255) страший адрес ассета
                
                ; сохранение первого байта в буфере спрайтов (Adr.SpriteInfoBuffer)
                LD E, C                                                         ; индекс ассета (0-127)
                LD A, (DE)                                                      ; чтение страницы памяти (номер страницы 0-31)
                OR B
                ; -----------------------------------------
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | O2 | O1 | O0 | P4 | P3 | P2 | P1 | P0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   O2-O0   [7-3]       - смещение в блоковой таблице
                ;   P4-P0   [3-0]       - страница памяти
                ; -----------------------------------------
                LD (HL), A
                INC L

                ; сохранение второго байта в буфере спрайтов (Adr.SpriteInfoBuffer)
                SET 7, E
                LD A, (DE)                                                      ; чтение старшего адреса
                LD (HL), A
                INC HL
                ; -----------------------------------------

.NextHextile    EXX
                DJNZ .Loop
                RET

                display " - Initialize graphics packages:\t\t\t", /A, Initialize.GraphicsPackages, "\t= busy [ ", /D, $-Initialize.GraphicsPackages, " byte(s)  ]"

                endif ; ~_MODULE_SESSION_INITIALIZE_GRAPHICS_PACKAGES_