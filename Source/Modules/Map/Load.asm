
                ifndef _MODULE_MAP_LOAD_
                define _MODULE_MAP_LOAD_
; -----------------------------------------
; загрузка карты
; In:
;   SP+0 - первый параметр
; Out:
; Corrupt:
; Note:
; -----------------------------------------
Load:           ; -----------------------------------------
                ; загрузка ресурса карты
                ; -----------------------------------------
                POP HL                                                          ; ID загружаемой карты
                SET_PAGE_ASSETS                                                 ; включить страницу расположения ассет менеджера
                LD A, L
                PUSH AF
                LOAD_ASSETS_A                                                   ; загрузка ресурса
                                                                                ;   HL - адрес загрузки/распаковки
                ; -----------------------------------------
                ; парсинг FMapHeader
                ; -----------------------------------------

                ; сохранение информации о карте
                LD DE, GameSession.MapInfo
                LD BC, FMapInfo
                LDIR

                ; загрузка ассета ID тайлового биома
                PUSH_PAGE                                                       ; сохранение номера страницы в стеке
                LD E, (HL)
                INC HL
                SET_PAGE_ASSETS                                                 ; включить страницу расположения ассет менеджера
                SET_LOAD_ASSETS_REG E, Page.TileSprites, Adr.TileSprites        ; принудительная установка места загрузки ресурса
                PUSH HL
                LOAD_ASSETS_REG E                                               ; загрузка ресурса графика тайлов биома
                POP HL
                POP_PAGE                                                        ; восстановление номера страницы из стека

                ; чтение длины блока данных биома (тайлы)
                LD C, (HL)
                INC HL
                LD B, (HL)
                INC HL

                ; расчёт адреса до данных биома (тайлы)
                LD E, (HL)
                INC HL
                LD D, (HL)
                ADD HL, DE
                ; -----------------------------------------
                ; копирование данных между страницами
                ; In:
                ;   A HL - адрес исходника  (аккумулятор страница)
                ;   A'DE - адрес назначения (аккумулятор страница)
                ;   BC   - длина блока
                ; Out:
                ; Corrupt:
                ; Note:
                ; -----------------------------------------
                LD A, Page.BiomeBuf
                LD DE, Adr.BiomeBuf
                EX AF, AF'
                CALL GetPage                                                    ; получение текущей страницы
                CALL Memcpy.BetweenPages
                
                ; -----------------------------------------
                ; освобождение ресурса карты
                ; -----------------------------------------
                SET_PAGE_ASSETS                                                 ; включить страницу расположения ассет менеджера
                POP AF
                JP_RELEASE_ASSET_A

                endif ; ~_MODULE_MAP_LOAD_
