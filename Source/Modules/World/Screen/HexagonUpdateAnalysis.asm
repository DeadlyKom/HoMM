
                ifndef _MODULE_WORLD_SCREEN_REFRESH_HEXAGON_UPDATE_ANALYSIS_
                define _MODULE_WORLD_SCREEN_REFRESH_HEXAGON_UPDATE_ANALYSIS_
; -----------------------------------------
; анализ обновления гексагонов
; In:
; Out:
; Corrupt:
; Note:
; -----------------------------------------
HexUpdateAnalysis:
                ;       разбиение игрового окна на блоки копирования
                ;   позволяет оптимизировать вывод и не обновлять весь экран одновременно
                ;   в статическом виде, но с анимациями
                ;   исключение — полное обновление игровой области:
                ;   при скролле обновляются все буферы,
                ;   и игровое окно необходимо перерисовать полностью
                ;
                ;       разметка игрового окна на блоки 6×6 знакомест,
                ;   так как это подходит для стековой переброски POP/PUSH
                ;   также присутствуют области 4×6, 6×4 и 4×4
                ;
                ;       6      6      6     4
                ;   x------x------x------x---->|
                ;   |      |      |      |     |
                ;
                ;   +------+------+------+-----+   ---x
                ;   |      |      |      |     |      |  6
                ;   |      |      |      |     |      |
                ;   +------+------+------+-----+   ---x
                ;   |      |      |      |     |      |  6
                ;   |      |      |      |     |      |
                ;   +------+------+------+-----+   ---x
                ;   |      |      |      |     |      |  6
                ;   |      |      |      |     |      |
                ;   +------+------+------+-----+   ---x
                ;   |      |      |      |     |      |  4
                ;   +------+------+------+-----+   ---x
                ;
                ;   ⚠️ Важно:
                ;    - гексагоны рисуются снизу вверх
                ;    - гексагон имеет ограничение 6×8 знакомест
                ;
                ;   гексагонная сетка:
                ;    __    __    __    __    __
                ;   /  \__/  \__/  \__/  \__/  \
                ;   \__/  \__/  \__/  \__/  \__/  <- начальная строка рисования гексагона
                ;   /  \__/  \__/  \__/  \__/  \
                ;   \__/  \__/  \__/  \__/  \__/
                ;   /  \__/  \__/  \__/  \__/  \
                ;   \__/  \__/  \__/  \__/  \__/
                ;   /  \__/  \__/  \__/  \__/  \
                ;   \__/  \__/  \__/  \__/  \__/
                ;
                ;   ℹ️ во время отображения каждая новая строка
                ;   начинается на 4 знакоместа ниже
                ;   при выводе в буфер рендера (RenderBuffer)
                ;   записывается информация о высоте каждого знакоместа
                ;   если знакоместо высокое — бит хранит 1, иначе — 0
                ;   это необходимо для наложения сглаживающего тумана
                ;   также по этим данным можно определить высоту
                ;   структура столбца гексагона:
                ;   бит 7 — самое нижнее знакоместо,
                ;   бит 0 — самое высокое
                ;   при сканировании битов с 7 по 0 (LSB → MSB)
                ;   ищем первый встречающийся включенный бит, номер бита 
                ;   обозначает высоту данной колонки гексагона

                LD HL, Adr.ScreenBlock
                LD D, H
                LD E, L
                INC E
                LD (HL), #00
                LDI
                LDI
                LDI
                LDI
                LDI
                LDI
                LDI
                LDI
                LDI
                LDI
                LDI
                LDI
                LDI
                LDI
                LDI
                LDI

                ; -----------------------------------------
                ; представление дисплейного списка
                ; +0 - первая рисуемая колонка первого гексагона (0-5)
                ; +1 - индекс/смещение в рендер буфере обрабатываемого гексагона
                ; +2 - индекс/смещение в рендер буфере обрабатываемого гексагона +80
                ; +3 - номер строки по вертикали в пикселях
                ; +4 - количество пропускаемых знакомест (спрайт находится ниже экрана)
                ; -----------------------------------------
                ; инициализация
                LD H, HIGH Adr.RenderBuffer
                LD D, HIGH Adr.Mod6Table
                EXX
                LD IX, (GameState.DisplayList)
                LD D, HIGH Adr.RenderBuffer                                     ; для чтения бит высоты столбца

                ; формирование цикла по вертикали (строк гексагонов)
                LD BC, .RowsLoop
                LD A, (GameState.DisplayListLen)
                NEG
                ADD A, #08
                LD (.RowsJump), A
.RowsJump       EQU $+1
                JR $
                PUSH BC
                PUSH BC
                PUSH BC
                PUSH BC
                PUSH BC
                PUSH BC
                PUSH BC

.RowsLoop       ; цикл отображения строк гексагонов

                ; переход к следующему элементу списка отображения
                LD BC, -Size.DisplayList.ElementSize
                ADD IX, BC
                
                ; -----------------------------------------
                ; чтение индекса/смещения в рендер буфере
                EXX
                LD L, (IX + 1)                                                  ; индекс/смещение в рендер буфере обрабатываемого гексагона
                DEC L                                                           ; предварительный, перед началом цикла
                EXX
                LD E, (IX + 2)                                                  ; индекс/смещение в рендер буфере обрабатываемого гексагона +80

                ; -----------------------------------------
                LD B, SCR_WORLD_SIZE_X                                          ; ширина строки (константна)
                LD C, (IX + 0)                                                  ; номер первой рисуемой колонки первого гексагона (0-5)
                RES 7, C                                                        ; сброс флага смещения по вертикали

.HexagonLoop    ; цикл отображения гексагонов
                EXX
                INC L                                                           ; переход к следующему гексогону в рендер буфере
                BIT RENDER_FLAG_HEX_UPDATE_BIT, (HL)
                JR Z, .ToNextHexagon                                            ; переход, если нет необходимости обновлять гексагон (частично или полностью)
                RES RENDER_FLAG_HEX_UPDATE_BIT, (HL)
                EXX

                ; формирование цикла по горизонтали (колонки гексагона)
                LD HL, .NextHexagon
                PUSH HL
                LD HL, .ColumnLoop
                LD A, C
                LD C, #00                                                       ; сброс счётчика отрисованных колонок гексангона
                LD (.ColumnJump), A
.ColumnJump     EQU $+1
                JR $
                PUSH HL
                PUSH HL
                PUSH HL
                PUSH HL
                PUSH HL
    
                ;   DE - адрес Adr.RenderBuffer + 80 (для чтения бит высоты столбца)
                ;   B  - оставшиеся ширина строки
                ;   C  - счётчика отрисованных колонок гексангона (сброшены)
                ;   IX - адрес списка отображения
    
.ColumnLoop     ; цикл колонок гексагона
                ; таблица для поиска первого установленного бита (LSB → MSB)
                LD A, (DE)
                LD L, A
                LD H, HIGH Adr.BitScanLsbTable
                ; определение строки экранного блока
                LD A, (IX + 3)                                                  ; номер строки по вертикали в пикселях (младшие 3 бита всегда включены)
                SUB #0F                                                         ; вычитаем младшие 3 бита, а так же перемещаемся на 0-ю строку экрана
                                                                                ; и результат нужно было бы сдвинуть на 3 бита влево, но этого не требуется xD
                OR (HL)                                                         ; высота колонки (0-7, нулевого значение для таблицы)
                ;    ℹ️ структура байта
                ;       7    6    5    4    3    2    1    0
                ;    +----+----+----+----+----+----+----+----+
                ;    | S4 | S3 | S2 | S1 | S0 | H2 | H1 | H0 |
                ;    +----+----+----+----+----+----+----+----+
                ;    
                ;    S4-S0   [7-3]       - номер строки в знакоместах
                ;    H2-H0   [3-0]       - высота гексагона

                ; расчёт адреса таблицы
                ADD A, LOW Adr.ScrBlockTable
                LD L, A
                LD H, HIGH Adr.ScrBlockTable

                ; вычисление номера текущего screen block'а (B % 6)
                LD A, SCR_WORLD_SIZE_X
                SUB B
                ADD A, C
                EXX
                EX DE, HL
                LD L, A
                LD A, (HL)  ; чтение B % 6 (0-3)
                EX DE, HL
                EXX
                ADD A, A    ; x2
                ADD A, A    ; x4
                ; -----------------------------------------
                ;    ℹ️ структура байта
                ;       7    6    5    4    3    2    1    0
                ;    +----+----+----+----+----+----+----+----+
                ;    | H1 | H0 | 0  | 0  | 0  | 0  | S1 | S0 |
                ;    +----+----+----+----+----+----+----+----+
                ;    
                ;    S1-S0   [1,0]       - смещение в массиве screen block'ов
                ;    H1,H0   [7,6]       - количество пересекаемых screen block'ов (0,1,2)
                ;                           0 - screen block 0-3
                ;                           1 - screen block 4-7
                ;                           2 - screen block 8-11
                ;                           3 - screen block 12-15
                ; -----------------------------------------
                OR (HL)                                                         ; добавление горизонтального смещения (0-3)
                AND %00111111                                                   ; обнуление пересекаемых screen block'ов
                EX AF, AF'                                                      ; сохранение смещения в массиве screen block'ов
                
                ; формирование цикла инкрементов затронутых screen block'ов
                LD A, (HL)
                RLCA
                RLCA
                AND %00000011
                ADD A, A    ; x2
                LD (.IncrementJump), A

                ; рассчёт адреса screen block'а
                EX AF, AF'                                                      ; восстановление смещение в массиве screen block'ов
                ADD A, LOW Adr.ScreenBlock
                LD L, A
                LD H, HIGH Adr.ScreenBlock

                ; инремент счётчиков обновлений
.IncrementJump  EQU $+1
                JR $

                INC (HL)
                DEC L
                INC (HL)
                DEC L
                INC (HL)
                DEC L

                ; следующая колонка гексагона
                INC E                                                           ; слещующая колонка
                INC C                                                           ; увеличить счётчик обработанных колонок гексангона

                RET

.ToNextHexagon  ; переход к следующему гексагону
                EXX

                ; C - номер колонки с которой рисуется гексагон
                ; расчитываем оставшиеся количество колонок в гексагоне
                LD A, HEXTILE_SIZE_X
                SUB C
                LD C, A                                                         ; ширина гексагона в ренер буфере (0-5)

                ; следующий гексагон
                ADD A, E
                LD E, A

.NextHexagon    ; следующий гексагон

                ; HL' - адрес обрабатываемого гексагона в рендер буфере
                ; D'  - старший адрес таблицы mod чиста (0-21)
                
                LD A, B
                SUB C                                                           ; счётчика отрисованных колонок гексангона
                RET Z                                                           ; выход, если был отрисована последний гексагон в строке
                
                LD C, #00
                LD B, A
                CP HEXTILE_SIZE_X
                JP NC, .HexagonLoop                                             ; переход, если можно отобразить гексагон целиком

                LD A, HEXTILE_SIZE_X
                SUB B
                LD C, A
                JP .HexagonLoop

                display " - Hexagon update analysis:\t\t\t\t", /A, HexUpdateAnalysis, "\t= busy [ ", /D, $ - HexUpdateAnalysis, " byte(s)  ]"

                endif ; ~_MODULE_WORLD_SCREEN_REFRESH_HEXAGON_UPDATE_ANALYSIS_
