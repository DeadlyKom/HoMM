
                ifndef _CHUNK_ARRAY_AREA_
                define _CHUNK_ARRAY_AREA_
; -----------------------------------------
; получение значений в области
; In:
;   HL - адрес счётчиков массива чанков (выровненый 256 байт)
;   DE - координаты области в тайлах (D - y, E - x)
;   BC - размер охватываемой области в чанках (B - y, C - x)
;   IX - адрес обработчика сортировки
;       A - количествой добавляемых элементов
;       H - старший адрес текущего массива чанков
;       E - смещение в массиве чанков первого элемента
; Out:
; Corrupt:
;   L, DE, BC, AF
; Note:
; -----------------------------------------
Area:           ; получение количество проходимых чанков до следующей строки
                LD A, C
                AND CHUNK_SIZE_MASK
                ADD A, A    ; x2
                LD (.Jump), A

                ; -----------------------------------------
                ; получение номера чанка
                ; In:
                ;   DE - координаты в тайлах (D - y, E - x)
                ; Out:
                ;   A  - порядковый номер чанка
                ; Corrupt:
                ;   D, AF
                ; Note:
                ; -----------------------------------------
                CALL GetChunkIndex

                LD D, B
                LD E, C
                
                ; -----------------------------------------
                ; получение адреса указаннго чанка
                ; In:
                ;   A  - порядковый номер чанка
                ;   HL - адрес счётчиков массива чанков (выровненый 256 байт)
                ; Out:
                ;   HL - начальный адрес счётчика в указанном чанке
                ;   A  - количествой пройденых элементов/начальный адрес расположения элементов
                ;   B  - обнулён
                ; Corrupt:
                ;   L, BC, AF
                ; Note:
                ; -----------------------------------------
                CALL GetAddress
                LD C, E
                LD E, A                                                         ; сохранение индекса элемента

                ; HL - адрес счётчиков массива чанков (выровненый 256 байт)
                ; D  - высота охватываемой области в чанках
                ; E  - количество пройденых элементов в чанке
                ; C  - ширина охватываемой области в чанках

.Loop_Y         LD B, C
.Loop_X         ; -----------------------------------------
                ; чтение данных массива чанков
                LD A, (HL)                                                      ; количество элементов в чанке
                ; проверка наличия элементов в массиве
                OR A
                CALL NZ, .Sort
                ; -----------------------------------------

                ; следующий чанк
                LD A, E
                ADD A, (HL)
                INC L
                LD E, A

                DJNZ .Loop_X

                ; переход к следующей строке в массиве чанков
                LD A, E
.Jump           EQU $+1
                JR $
                rept CHUNK_SIZE
                ADD A, (HL)
                INC L
                endr
                LD E, A

                DEC D
                JR NZ, .Loop_Y
                RET

.Sort           ; A - количествой добавляемых элементов
                ; H - старший адрес текущего массива чанков
                ; E - смещение в массиве чанков первого элемента
                JP (IX)

                endif ; ~ _CHUNK_ARRAY_AREA_
