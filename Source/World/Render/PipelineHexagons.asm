
                ifndef _WORLD_RENDER_PIPELINE_HEXAGONS_
                define _WORLD_RENDER_PIPELINE_HEXAGONS_
; -----------------------------------------
; конвейера отображения гексагонов
; In:
; Out:
; Corrupt:
; Note:
; -----------------------------------------
PipelineHexagons:
                CALL Convert.SetBaseScreen                                      ; установка работы с основным экраном
                ; -----------------------------------------
                ; ⚠️ холодный запуск:
                ;    1. принудительно выставить адрес левого верхнего угла отображаемой карты
                ;       данные о центрировании карты могут браться из метаданных карты и/или иным способом
                ;    2. принудительное копирование в Tilemap-буфер из тайловой карты на странице 1
                ;    3. принудительное выставление флагов в Render-буфере
                ;       формируется для каждого тайла
                ;
                ; ⚠️ горячий запуск:
                ;    не должен самовозбуждать флаги обновления в Render-буфере,
                ;    дабы снизить нагрузку на обновление экрана
                ;
                ; ℹ️ цикл обновления экрана:
                ;    1. в выставленные тайминги скролла проверить вектор движения карты,
                ;       обновить адрес левого верхнего угла отображаемой карты
                ;    2. принудительное обновление Tilemap- и Render-буферов по необходимости,
                ;       если адрес левого верхнего угла отображения карты изменился
                ;       * Tilemap-буфер копируется из тайловой карты на странице 1
                ;       * Render-буфер формируется для каждого тайла, где:
                ;         - выставляется флаг HU (принудительного обновления гексагона)
                ;         - копируется флаг FG (тумана, 0 — гексагон закрашен туманом целиком)
                ;           из буфера метаданных карты
                ;         - номер анимации гексагона устанавливается в ноль,
                ;           в последующих шагах он будет модифицирован
                ;       * Render-буфер для каждого столбца гексагона выставляется флаг CU
                ;           необходимости обновить столбец гексагона (0 - обновление столбца не требуется)
                ;    3. применение внешних событий изменения/обновления гексагонов
                ;       * основной список гексагон-объектов,
                ;         которые обязаны менять анимацию каждый тик гексагонов
                ;         каждый такой объект имеет информацию о количестве анимаций в цикле
                ;       * дополнительный список гексагон-объектов,
                ;         которые рандомно выбираются из доступных на экране
                ;         каждый такой объект после проигрывания одного цикла анимации
                ;         удаляется из списка
                ;    4. отображение гексагонов
                ;    5. отображение объектов на карте
                ;       * список динамических объектов влияет на флаг HU (принудительного обновления гексагона)
                ;         спрайты героя, VFX заклинаний и т.п.
                ;    6. отображение тумана на видимых гексагонах
                ;    7. копирование узора рамки на игровой части экрана
                ;    8. анализ флагов HU (принудительного обновления гексагона) и высот гексагонов,
                ;       позволяет определить, какие экранные блоки требуется копировать в теневой экран
                ;       * выставленный флаг HU говорит о том, что гексагон изменился
                ;         используя высоты гексагона, можно определить,
                ;         какие соседние экранные блоки были задеты,
                ;         и на их основе формируются флаги обновления грязных экранных блоков
                ;       * экранные блоки копируются в теневой экран
                ;
                ; ℹ️ цикл обновления курсора:
                ;    1. если не требуется копирование из основного экрана в теневой:
                ;       ⚠️ важно: обязательно в начале прерывания (избежать сечения луча)
                ;       * восстанавливаем фон под курсором (в теневом экране), если он имеется
                ;       * отображаем курсор в новой позиции (в теневом экране)
                ;       ℹ️ ToDo: можно избежать этого пункта,
                ;           если предыдущая анимация и положение не изменились
                ;       * любая логика прерывания
                ;    2. буферный экран (основной) готов копироваться в теневой экран
                ;       * включить в адресном пространстве #C000–#FFFF страницу 7
                ;         (с теневым экраном)
                ;       * отображаем курсор в новой позиции (в основном экране)
                ;       * переключаемся на отображение основного экрана
                ;         (теневой экран перестаёт быть видимым)
                ;       * на странице 7 расположен код копирования экранных блоков,
                ;         буфер под курсор и другие необходимые массивы
                ;         (гексагон-объектов и т.п.)
                ;         копируются блоки экрана в теневой
                ;         (ℹ️ сечение луча не страшен)
                ;       * отображаем курсор в новой позиции (в теневом экране)
                ;       * переключаемся на отображение теневого экрана
                ;         (основной экран перестаёт быть видимым)
                ;       * восстановить фон в основном экране под курсором
                ;       * сброс флагов готовности экрана
                ; -----------------------------------------
                SET_PAGE_OBJECT                                                 ; включить страницу работы с объектами
                CALL World.Base.Render.Object.InView                            ; формирование списка объектов в области видимости
                PUSH AF
                CALL NZ, World.Base.Render.Object.DirtyEnvir                    ; анализ видимых объектов и выставление флагов гексагонов, которые требуется перерисовать
                
                SET_PAGE_MAP                                                    ; включить страницу работы с картой
                RESTORE_BC                                                      ; защитная от порчи данных с разрешённым прерыванием
                CALL Draw.HexByDL
                SET_PAGE_SCREEN_SHADOW                                          ; включение страницы теневого экрана
                CALL ScreenBlock.HexAnalysis                                    ; анализ обновления гексагонов

                POP AF
                CALL NZ, Object.Draw                                            ; отображение объектов в массиве SortBuffer

                SET_MODULE_PAGE_World                                           ; включить страницу модуля "World"
                CALL World.Display.GameWindow.Ornament
                SET_PAGE_SCREEN_SHADOW                                          ; включение страницы теневого экрана
                 
                ; установка флага готовности кадра и долгого переключения экранов
                RENDER_FLAGS
                SET_FLAG FRAME_READY_BIT
                SET_FLAG SWAP_PENDING_BIT
                RES_VIEW_FLAG FORCED_FRAME_UPDATE_BIT                           ; сброс флага принудительного обновления кадра
                RET

.Swap           SET_RENDER_FLAG SWAPPED_PENDING_BIT                             ; установить флаг успеного переключения экрана, ожидаем окончание прерывания
                JP_SHOW_BASE_SCREEN                                             ; отображение базового экрана

.MemcpyScreen   RESTORE_SCR World.Base.Interrupt.Memcpy
                
                ; ToDo: мб такая ситуация, когда за время копирование, данные в буфере фона курсора устарели
                ;       придётся их копировать в другой и восстанавливать из него

                ; 0 байт хранит данные о размере заполнения
                LD HL, Adr.CursorStorageA
                LD DE, Adr.CursorStorageB
                LD B, #00
                LD C, (HL)
                LD A, C
                OR A
                CALL NZ, Memcpy.FastLDIR

                ; копирование блоков, если курсор находится в игровой зоне, он скопируется тоже
                CALL ScreenBlock.Memcpy                                         ; копирование screen block'и в теневой экран

                ; принудительно меняем адрес на теневой буфер,
                ; затераем возможное копирование курсора
                LD HL, Adr.CursorStorageB+1
                LD E, (HL)
                INC L
                LD D, (HL)
                SET 7, D
                CALL Draw.RestoreByScrAdr                                       ; восстановление фона под курсором в указанном адресе экрана (только для OR_XOR_SAVE)

                RESTORE_SCR_
                RES_RENDER_FLAGS SWAPPED_PENDING | SWAP_PENDING                 ; сброс флага переключения экранов
                RET

                display " - Pipeline hexagons:\t\t\t\t\t", /A, PipelineHexagons, "\t= busy [ ", /D, $-PipelineHexagons, " byte(s)  ]"

                endif ; ~_WORLD_RENDER_PIPELINE_HEXAGONS_
