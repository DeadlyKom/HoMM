
                ifndef _WORLD_RENDER_OBJECT_UI_DRAW_
                define _WORLD_RENDER_OBJECT_UI_DRAW_
; -----------------------------------------
; отображение объекта UI
; In:
;   IY - адрес структуры объекта (FObjectUI)
; Out:
; Corrupt:
; Note:
; -----------------------------------------
Draw:           ; --------------------------------------------------------------
                LD HL, Indexes
                ; -----------------------------------------
                ; Note:
                ;   для ассета ASSETS_ID_UI_ARROW_PATH_PACK,
                ;   поле FObjectUI.Super.Sprite имеет слудующие значения:
                ;
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | .. | .. | A4 | A3 | A2 | A1 | A0 | PI |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   A4-A0   [5..1]  - индекс спрайта стрелки
                ;   PI      [0]     - флаг типа спрайта
                ;                       0 - проходимый
                ;                       1 - непроходимый
                ; -----------------------------------------

                ; определение индекса хранения проходимый/непроходимый
                LD B, (IY + FObjectUI.Super.Sprite)
                SRL B
                JR NC, .Draw                                                    ; переход, если спрайт проходимый
                INC HL

.Draw           ; --------------------------------------------------------------
                ; отображение спрайта

                ; расчёт адреса структуры FSpritesRef
                LD A, (HL)
                ADD A, A    ; x2                                                ; старщий флаг игнорируем, т.к. ставим его самостоятельно
                LD L, A
                LD H, HIGH Adr.SpriteInfoBuffer >> 2
                ADD HL, HL  ; x4
                ADD HL, HL  ; x8

                LD A, B                                                         ; номер анимации
                SCF                                                             ; указываем на структуру FSpritesRef
                JP Draw.Sprite

                endif ; ~_WORLD_RENDER_OBJECT_UI_DRAW_
