
                ifndef _WORLD_RENDER_OBJECT_HERO_DRAW_
                define _WORLD_RENDER_OBJECT_HERO_DRAW_
; -----------------------------------------
; отображение объекта "герой"
; In:
;   IX - адрес функции обработки спрайта
;   IY - адрес структуры объекта (FObjectHero)
; Out:
; Corrupt:
; Note:
; -----------------------------------------
Bound:          LD IX, Utilities.SpriteBound
; -----------------------------------------
; отображение объекта "герой"
; In:
;   IY - адрес структуры объекта (FObjectHero)
; Out:
; Corrupt:
; Note:
; -----------------------------------------
Draw:           ; --------------------------------------------------------------
                LD HL, Indexes

                ; -----------------------------------------
                ;   FObjectHero.Super.Sprite
                ;      7    6    5    4    3    2    1    0
                ;   +----+----+----+----+----+----+----+----+
                ;   | S1 | S0 | D2 | D1 | D0 | A2 | A1 | A0 |
                ;   +----+----+----+----+----+----+----+----+
                ;
                ;   S1,S0   [7,6]   - состояние анимации
                ;                       00 - 'бездействие'
                ;                       01 - 'перемещение'
                ;                       10 - 'поворот', A2-A0 хранит требуемое направление
                ;                       11 - резерв
                ;   В2-В0   [5..3]  - направление спрайта
                ;                       000 - вверх
                ;                       001 - вверх-вправо
                ;                       010 - вправо
                ;                       011 - вниз-вправо
                ;                       100 - вниз
                ;                       101 - вниз-влево
                ;                       110 - влево
                ;                       111 - вверх-влево
                ;   A2-A0   [2..0]  - индекс анимации спрайта
                ; -----------------------------------------
                LD C, (IY + FObjectHero.Super.Sprite)

                ; направление спрайта
                LD A, C
                RRA
                RRA
                RRA
                AND DIR_MASK
                LD B, A

                ; проверка состояния анимации
                BIT ANIM_STATE_BIT, C                                           ;   0 - бездействие/поворот
                                                                                ;   1 - перемещение
                JR Z, .Draw                                                     ; перейти, если состояние анимации бездействие

.Move           ; применить направление спрайта анимации
                LD A, B
                INC A                                                           ; пропуск idle индекса спрайта
                ADD A, L
                LD L, A
                ADC A, H
                SUB L
                LD H, A

                ; номер анимации
                LD A, C
                AND %00000111
                LD B, A

.Draw           ; --------------------------------------------------------------
                ; отображение спрайта

                ; расчёт адреса структуры FSpritesRef
                LD A, (HL)
                ADD A, A    ; x2                                                ; старщий флаг игнорируем, т.к. ставим его самостоятельно
                LD L, A
                LD H, HIGH Adr.SpriteInfoBuffer >> 2
                ADD HL, HL  ; x4
                ADD HL, HL  ; x8

                LD A, B                                                         ; номер анимации
                SCF                                                             ; указываем на структуру FSpritesRef
                JP (IX)

                endif ; ~_WORLD_RENDER_OBJECT_HERO_DRAW_
